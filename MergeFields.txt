' MacroName:MergeFields.2025.02
' MacroDescription:Merges two consecutive fields in a bibliographic record.
'
' This macro was written by Walter F. Nickeson and last updated 3 May 2025.
' It was developed in Connexion client 3.1 running in Windows 11 Enterprise and 64-bit
' Windows 10 Pro.
' Walt's macros for the Connexion client: https://github.com/wnickeson/WaltsMacros
' wfnickeson@zohomail.com
'
' Copyright 2025 Walter F. Nickeson.
'
' Permission is hereby granted, free of charge, to any person obtaining a copy of this
' software and associated documentation files (the "Software"), to deal in the Software
' without restriction, including without limitation the rights to use, copy, modify,
' merge, publish, distribute, sublicense, and/or sell copies of the Software, and to
' permit persons to whom the Software is furnished to do so, subject to the following
' conditions:
'
' The above copyright notice and this permission notice shall be included in all copies
' or substantial portions of the Software.
'
' THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED,
' INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A
' PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT
' HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF
' CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR
' THE USE OR OTHER DEALINGS IN THE SOFTWARE.
'
'****************************************************************************************
' How it works: This macro combines two consecutive variable fields in a bibliographic
' record, such as when contents notes have been divided into several fields and you want
' them to be in only one. Click anywhere in the first field to be merged and run the
' macro. It combines that field with the field immediately following, and the resulting
' merged field replaces the first one, keeping its tag and indicators. The second field
' is deleted. The last three characters of the original field are highlighted, making it
' easy to see where the fields were joined.
'
' Subfield coding in both fields is preserved. This means that if the first subfield of
' the second field is subfield $a (which Connexion normally does not display), and the
' last subfield of the first field is something else, the macro adds "$a" at the head of
' the information in the second field. If the merged fields are contents notes (tag 505),
' the content of the two fields will be separated by the ISBD dash of space-hyphen-hyphen
' space. Otherwise, the content of the merged fields will be separated by a space. No
' information is lost in the merge except for the tag and indicators of the second field.
'
' The macro always asks for confirmation before proceeding. If the two fields have the
' same tag, the default option is to go ahead, but if their tags differ, the default
' option is to cancel the macro. No claim can be made about the validity of the new field
' resulting from the merge, especially if two different types of fields are combined!
'
' The macro can't merge fields containing non-Latin script. It also can't tell if the
' characters in the field are properly encoded as MARC-8, so it is possible that some
' characters will get mangled in processing. Use my macro "Decompose" to correct the
' encoding in the fields to be merged so the results will be as expected.
'
' The limit of the length of a variable field in MARC is 9,999 characters (strictly
' speaking, octets). The macro won't merge the two fields if their combined length
' exceeds that limit.
'****************************************************************************************

Option Explicit

'****************************************************************************************

Sub Main

Dim CS As Object
On Error Resume Next
Set CS = GetObject( , "Connex.Client" )
On Error GoTo 0
If CS Is Nothing Then Set CS = CreateObject( "Connex.Client" )

Const CRITICAL_MESSAGE     As Integer = 16
Const MSGBOX_RETURN_CANCEL As Integer = 2

Dim DELIMITER   As String*1  : DELIMITER    = Chr$( 223 )
Dim DELIMITER_A As String*2  : DELIMITER_A  = DELIMITER & "a"

Dim ButtonSet%
Dim EndOfField1Pos%
Dim Field1$
Dim Field1LastChars$
Dim Field1Row%
Dim Field1Tag$
Dim Field2$
Dim Field2Tag$
Dim LastSubfieldCodeField1$
Dim LenField1%
Dim LenMergedFields%
Dim MergedFields$
Dim MergeQuery$
Dim Start%
Dim TypeOfWindow%
Dim WaltsMacros$             : WaltsMacros$ = "[Walt's macros] Extras2:MergeFields"

Dim p As Integer

' First, make sure a bibliographic record is displayed.

TypeOfWindow% = CS.ItemType
Select Case TypeOfWindow%
  Case -1, 3 To 16, 18, 20 To 26
    MsgBox "Sorry, this macro works only in a bibliographic record!", CRITICAL_MESSAGE, WaltsMacros$
    Exit Sub
End Select

Field1Row% = CS.CursorRow

If CS.GetField( "...", Field1Row%, Field1$ ) Then
    If Field1$ = "Data contains non-latin script" Then
        MsgBox "Sorry, this macro works only with fields in Latin script.", CRITICAL_MESSAGE, WaltsMacros$
        Exit Sub
      Else
        LenField1%  = Len( Field1$ )
        Field1Tag$ = Left$( Field1$, 3 )

' Find the last subfield code in the first field. If it's not subfield $a, then "$a" will
' be added to the beginning of the next field in the merge, unless the first subfield in
' the second field is explicitly coded as something other than $a.

        Start% = 1
        Do
          p = InStr( Start%, Field1$, DELIMITER )
          If p > 0 Then
              LastSubfieldCodeField1$ = Mid$( Field1$, p + 1, 1 )
            Else
              If Start% = 1 Then
                  LastSubfieldCodeField1$ = DELIMITER_A
              End If
          End If
          Start% = p + 1
        Loop Until p = 0
    End If

    If CS.GetField( "...", Field1Row% + 1, Field2$ ) Then
        If Field2$ = "Data contains non-latin script" Then
            MsgBox "Sorry, this macro works only with fields in Latin script.", CRITICAL_MESSAGE, WaltsMacros$
            Exit Sub
        End If
        If Len( Field2$ ) - 5 + LenField1% > 9999 Then
            MsgBox "The size of the two fields combined will exceed the MARC limit of 9,999 characters! Macro exiting.", CRITICAL_MESSAGE, WaltsMacros$
            Exit Sub
        End If
        Field2Tag$ = Left$( Field2$, 3 )
        If Mid$( Field2$, 6, 1 ) <> DELIMITER Then
            If LastSubfieldCodeField1$ = DELIMITER_A Then
                Field2$ = Mid$( Field2$, 6 )
              Else
                Field2$ = DELIMITER_A & " " & Mid$( Field2$, 6 )
            End If
          Else
            Field2$ = Mid$( Field2$, 6 )
        End If
        If Field1Tag$ = Field2Tag$ Then
            MergeQuery$ = "Merge this " & Field1Tag$ & " field with the following " & Field2Tag$ & " field?"
            ButtonSet%  = 1 + 32        'OK, Cancel + Warning Query
          Else
            MergeQuery$ = "Do you really want to merge this " & Field1Tag$ & " with the next " & Field2Tag$ & " field?"
            ButtonSet%  = 1 + 32 + 256  'OK, Cancel + Warning Query + Second button default
        End If
        If MsgBox( MergeQuery, ButtonSet%, WaltsMacros$ ) = MSGBOX_RETURN_CANCEL Then
            Exit Sub
          Else

' Get the last three characters of the first field; after the fields are merged, they
' will be selected, or highlighted, to help quickly find the end of the field in case
' editing is needed, such as to delete punctuation.

            EndOfField1Pos%  = Len( Field1$ )
            Field1LastChars$ = Right$( Field1$, 3 )
            If Field1Tag$ = "505" and Field2Tag$ = "505" Then
                If Right$( Trim$( Field1$ ), 3 ) <> " --" Then
                    Field1$ = Field1$ & " -- "
                End If
             End If
             MergedFields$ = Field1$ & " " & Field2$
        End If
    End If
End If

' Replace the original first field with the new field containing all the merged fields.

If CS.SetFieldLine( Field1Row%, MergedFields$ ) = FALSE Then
    MsgBox "Sorry, something went wrong when trying to set the new field.", CRITICAL_MESSAGE, WaltsMacros$
    Exit Sub
End If

' Delete the second field.

If CS.DeleteFieldLine( Field1Row% + 1 ) = FALSE Then
    MsgBox "Sorry, something went wrong when trying to delete the merged field.", CRITICAL_MESSAGE, WaltsMacros$
End If

' Highlight or select the end of the original first field.

CS.CursorRow    = Field1Row%
CS.CursorColumn = EndOfField1Pos% - 4
If CS.FindText( Field1LastChars, TRUE ) Then
End If

End Sub
'2038782
