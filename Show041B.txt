'MacroName:Show041B.2023.02
'MacroDescription:Displays the language codes in an 041 field in a bibliographic record
' for guided entry or verification.
'
' This macro was written by Walter F. Nickeson,
' University of Rochester, Rochester, NY
' wnickeson@library.rochester.edu
'
' Thanks to Terry Simpkins in earlier days for clarifying discussion. However, all
' responsibility for this macro's appearance and functionality are mine alone.
'
' Last updated: 4 February 2023.
' Check for the latest versions of this and my other macros at
' https://github.com/wnickeson/WaltsMacros
' Please e-mail me with bug reports or to suggest improvements.
'
' This macro works for me with Connexion client 3.1 and 64-bit Windows 10 Enterprise &
' 64-bit Windows 10 Pro, but no guarantees are promised or implied.

'****************************************************************************************
' How it works: Run the macro to display an 041 field in a view with labeled subfields
' and a searchable list of languages, to aid in verification and editing of the field. If
' the macro is run with the cursor already in an 041 field, that field will be the one on
' display; otherwise, the macro displays the first 041 field in the record. If no 041
' field is present, the macro can help create one by offering guided input.
'
' The macro shows an 041 field in subfield groups. For each subfield, the macro displays
' a trio of text boxes, containing codes for the languages of the item, any original
' languages, and intermediate translations. Enter language codes in the text boxes, or
' browse or search the list of MARC languages in the dialog box to have codes adde
' directly. The only formatting requirement for input is that each language code in a
' text box be separated from the next by a single space. If multiple codes are found in a
' single subfield, as might be seen on an older record, the macro separates them
' automatically and can update the field without making any other changes.
'
' When searching for a language name, enter at least three characters. This will retrieve
' language codes as well as names. The shorter the search string entered, the more
' language names are likely to be retrieved. The list sorts the names by relevance.
' Whether a language name is found by a browse or by a search, the code for that language
' can be quickly added to the end of the selected subfield with a click of the mouse.

' The order of subfields in an existing field is maintained. New subfields added to the
' field appear following any subfields already present. When adding subfields, add them
' in the appropriate order for the item; the macro doesn't know enough to do that on its
' own. For any specific subfield containing the language codes of the item, the macro
' always keeps its associated subfields for related translation codes together. The only
' reordering done by this macro is in subfields $b and $f. In those subfields, language
' codes are put into alphabetical order.
'
' If subfields $h, $k, $m, or $n are present or added, the first indicator ("Item is or
' includes a translation") is set to "1", but of course whether or not the item is or
' includes a translation must be confirmed. These subfields can't be added at all if they
' follow a subfield code for which no language of original is allowed, such as for tables
' of contents (subfield $f).
'
' If the language code in the fixed field does not appear as the first language code in
' an 041 field, the macro offers a quick fix to set things right--to either change the
' fixed field to match the 041 field, or to change the first code in the 041 field to
' match the fixed field.
'
' The macro will not add a new 041 field consisting of only one language code, following
' OCLC policy.
'
' Finally, the macro works only with fields containing three-letter MARC language codes.
' Fields containing second indicator "7," stating that non-MARC codes are used, are
' rejected for consideration.
'****************************************************************************************

Option Compare Text
Option Explicit

Declare Sub MatchNamesToCodes  ( StringIn$ )
Declare Sub SearchLanguageNames( InString$ )
Declare Sub SetLanguageArrays

Declare Function FieldOutput             ( DelimSymbol$ )     As String
Declare Function FindCodesForNames       ( InputString$, _
                                           High%, _
                                           Low% )             As Integer
Declare Function FormatSubfieldsForOutput( InString$, _
                                           SubfCode$, _
                                           DelimSymbol$ )     As String
Declare Function SortSubfieldCodes       ( InString$ )        As String
Declare Function ToggleCodeIndex         ( InVal As Variant ) As Variant

Declare Function Dialog1ControlFunction( Id$, Action%, SVal& )

Type SubfieldStrings
 tInterLang        As String
 tOriginalLang     As String
 tOriginalLangCode As String
 tPrimaryLangs     As String
End Type

Global Const WARNING_MESSAGE      As Integer = 48
Global Const LANGUAGE_CODES_COUNT As Integer = 485
Global Const LANG_CODE_LENGTH     As Integer = 3

Global Const ARROW_PREFIX         As String  = "=>  "    'A prefix to a subfield in the list to show that it appears in the
                                                         ' field.
Global Const SPACE_PREFIX         As String  = "      "  'A prefix in the list of subfields to attempt to line them up, since
                                                         'OML has no way to align text within list boxes in a dialog box.

' Theses constants are for the subfields available in the drop-down list of subfields.
' There are no constants for subfields associated with translations, because those
' subfields can only exist in relation to one of the primary subfields below. Codes for
' the languages in these subfields, and for languages of the original and for
' intermediate translations, are contained together in the special record variable
' "SubfieldStrings."

Global Const aA As Integer = 0
Global Const aB As Integer = 1
Global Const aD As Integer = 2
Global Const aE As Integer = 3
Global Const aF As Integer = 4
Global Const aG As Integer = 5
Global Const aI As Integer = 6
Global Const aJ As Integer = 7
Global Const aP As Integer = 8
Global Const aQ As Integer = 9
Global Const aR As Integer = 10
Global Const aT As Integer = 11

Global CS As Object

Global FF_Lang$
Global FirstCode$
Global Ind1$
Global Ind2$
Global NewField$
Global OriginalField$
Global SubfieldsUsed$
Global TypeOfRecord%
Global WaltsMacros$

Global AddNew

Global DELIMITER    As String*1
Global DOUBLE_QUOTE As String*1

Global LanguageCodes   ( LANGUAGE_CODES_COUNT ) As String
Global LanguageNames   ( LANGUAGE_CODES_COUNT ) As String
Global RelatedSubfields( 2 )                    As String
Global ShowCodesNames  ()                       As String
Global SubfieldList    ( 11 )                   As String
Global Subfields       ( 11 )                   As SubfieldStrings

'****************************************************************************************

Sub Main

On Error Resume Next
Set CS = GetObject( , "Connex.Client" )
On Error GoTo 0
If CS Is Nothing Then Set CS = CreateObject( "Connex.Client" )

Const CRITICAL_MESSAGE     As Integer = 16
Const DIALOG_BUTTON_CANCEL As Integer = 102
Const INFORMATION_MESSAGE  As Integer = 64

Const FILL_CHAR            As Integer = 252

Dim ArrayIndex%
Dim BrowseListInstructions$
Dim CodeStringSplit%
Dim CurrArrayString$
Dim DataEntryInstructions$
Dim FFChangedNote$
Dim FF_Type$
Dim FieldData$
Dim LangCode$
Dim NextDelimiter%
Dim PosFF%
Dim PrevSubfieldCode$
Dim Row%
Dim SearchBoxInstructions$
Dim SubfieldCode$
Dim SubfieldSplit%
Dim SubfieldString$
Dim Tag$
Dim TempString$
Dim TestChar$
Dim TypeOfWindow%
Dim WorkformTest$

Dim EmptyField               : EmptyField = FALSE
Dim Found041                 : Found041   = FALSE
Dim In041                    : In041      = FALSE

Dim i As Integer, j As Integer, p As Integer, q As Integer

DELIMITER    = Chr$( 223 )
DOUBLE_QUOTE = Chr$( 034 )

WaltsMacros$ = "[Walt's macros] Extras1:Show041"

' The list of subfields available to edit. The other subfields valid in the field are for
' the original language and intermediate translations, but they can only appear following
' the actual language of the item in one of the subfields below. Note the variable
' spacing to try to align the columns of codes and descriptions; OML has no way to fine-
' tune layout in a dialog box on its own.

SubfieldList( 0  ) = "a - Language code of text/sound track or separate title"
SubfieldList( 1  ) = "b - Language code of summary or abstract"
SubfieldList( 2  ) = "d - Language code of sung or spoken text"
SubfieldList( 3  ) = "e - Language code of librettos"
SubfieldList( 4  ) = "f -  Language code of table of contents"
SubfieldList( 5  ) = "g - Language code of accompanying material other than librettos and transcripts"
SubfieldList( 6  ) = "i -  Language code of intertitles"
SubfieldList( 7  ) = "j -  Language code of subtitles"
SubfieldList( 8  ) = "p - Language code of captions"
SubfieldList( 9  ) = "q - Language code of accessible audio"
SubfieldList( 10 ) = "r -  Language code of accessible visual language"
SubfieldList( 11 ) = "t -  Language code of accompanying transcripts for audiovisual materials"

' Instructions for the dialog box.

DataEntryInstructions$  = "Select a subfield from the drop-down list above. Enter language codes directly, or browse or search"
DataEntryInstructions$  = DataEntryInstructions$ & " in the boxes below. Separate language codes in a subfield with spaces."
BrowseListInstructions$ = "Browse for a language name from this list:"
FFChangedNote$          = "The language code in the fixed field was changed from " & DOUBLE_QUOTE & FF_Lang$ & DOUBLE_QUOTE
FFChangedNote$          = FFChangedNote$ & " to " & DOUBLE_QUOTE & FirstCode & DOUBLE_QUOTE & "."
SearchBoxInstructions$  = "Or search for a language name or code (enter at least 3 characters):"

' First, make sure a bibliographic record is displayed.

TypeOfWindow% = CS.ItemType
Select Case TypeOfWindow%
  Case -1, 3 To 16, 18, 20 To 26
    MsgBox "Sorry, this macro works only in a bibliographic record!", CRITICAL_MESSAGE, WaltsMacros$
    Exit Sub
End Select

' If the cursor is already in an 041 field, get that field for processing; otherwise
' search for the first 041 field in the record. If an 041 field is found, check the
' second indicator to make sure the macro is going to encounter only MARC language codes.

Row% = CS.CursorRow
If CS.GetFieldLine( Row%, FieldData$ ) Then
    Tag$ = Left$( FieldData$, 3 )
    If Tag$ = "041" Then
        OriginalField$ = FieldData$
        Found041       = TRUE
        In041          = TRUE
        AddNew         = FALSE
      Else
        If CS.GetField( "041", 1, FieldData$ ) Then
            OriginalField$ = FieldData$
            Found041       = TRUE
            AddNew         = FALSE
          Else
            Found041       = FALSE
            AddNew         = TRUE
            EmptyField     = TRUE
        End If
    End If
End If

' Proceed by getting some fixed field values, which may mean altering and then restoring
' display of the fixed field. If the fixed field values being sought are unavailable or
' are fill characters, give them dummy values using the digit "9" (safe because the
' allowable values are letters).

PosFF% = CS.FixedFieldPosition
If PosFF% <> 1 Then CS.FixedFieldPosition = 1

If CS.GetFixedField( "Type", FF_Type$ ) Then
    If Asc( FF_Type$ ) = FILL_CHAR Then
        FF_Type$ = "9"
    End If
  Else
    FF_Type$ = "9"
End If
If CS.GetFixedField( "Lang", FF_Lang$ ) Then
    If Asc( Left$( FF_Lang$, 1 ) ) = FILL_CHAR Then
        FF_Lang$ = "999"
      Else
        If FF_Type$ Like "[ij]" Then
            TypeOfRecord% = aD
          Else
            TypeOfRecord% = aA
        End If
    End If
  Else
    FF_Lang$ = "999"
End If

CS.FixedFieldPosition = PosFF%

' Fill the two language arrays.

SetLanguageArrays

If Found041 Then

' If an 041 field is found, first see if it's empty, as it will be on a workform. If it
' is, jump down to the section to add a new field. Otherwise, proceed by checking the
' second indicator to make sure the field contains only MARC codes.

    Ind2$ = Mid$( FieldData$, 5, 1 )
    If Ind2$ = "7" Then
        MsgBox "Sorry, this macro deals only with MARC language codes.", CRITICAL_MESSAGE, WaltsMacros$
        Exit Sub
    End If
    Ind1$      = Mid$( FieldData$, 4, 1 )
    FieldData$ = Mid$( FieldData$, 6 )

    WorkformTest$ = Mid$( FieldData$, 6 )
    Do
      p = InStr( WorkformTest$, DELIMITER )
      If p <> 0 Then
          WorkformTest$ = Left$( WorkformTest$, p - 1 ) & Mid$( WorkformTest$, p + 2 )
      End If
    Loop Until p = 0
    If Trim$( WorkformTest$ ) = "" Then
        AddNew     = TRUE
        EmptyField = TRUE
    End If
'    FieldData$ = Trim$( Mid$( FieldData$, 5 ) )

' To aid in processing, make sure the field begins with a delimiter and "a", which OCLC
' hides by default if the first subfield is "a".

    If EmptyField = FALSE Then
        If Left$( FieldData$, 1 ) <> DELIMITER Then
            FieldData$ = DELIMITER & "a " & FieldData$
        End If

' Break up the field into its constituent subfields.

        SubfieldSplit% = 1
        NextDelimiter% = 1
        Do
          p = InStr( SubfieldSplit%, FieldData$, DELIMITER )
          If p <> 0 Then

        ' If a delimiter is found, look ahead to the next one (or to the end of the field), and
        ' take the text string between the two occurrences for manipulation

              NextDelimiter% = InStr( p + 1, FieldData$, DELIMITER )
              If NextDelimiter% = 0 Then
                  NextDelimiter% = Len( FieldData$ ) + 2
              End If
              SubfieldString$ = Mid$( FieldData$, p, NextDelimiter% - 1 - p )
              SubfieldCode$   = Mid$( SubfieldString$, 2, 1 )
              ArrayIndex%     = ToggleCodeIndex( SubfieldCode$ )
              SubfieldString$ = Trim$( Mid$( SubfieldString$, 3 ) )
              If Len( SubfieldString$ ) Mod 3 > 0 Then
                  MsgBox "Something's wrong in the 041 field"
                  Exit Sub
                Else
                  CodeStringSplit% = 1
                  Do
                    LangCode$ = Mid$( SubfieldString$, CodeStringSplit%, LANG_CODE_LENGTH )
                    If FirstCode$ = "" Then
                        FirstCode$ = LangCode$
                    End If
                    If SubfieldCode$ Like "[abdefgijpqrt]" Then
                        CurrArrayString$ = Subfields( ArrayIndex% ).tPrimaryLangs
                        If InStr( CurrArrayString$, LangCode$ ) = 0 Then
                             Subfields( ArrayIndex% ).tPrimaryLangs = CurrArrayString$ & " " & LangCode$
                        End If

' Assume, in an existing field, that codes for intermediate and original languages
' immediately follow the appropriate code. The subfield code for an intermediate
' translation is always "k", but the code for original languages depends on on which
' subfield it follows.

                      ElseIf SubfieldCode$ = "k" Then
                        For i = Len( SubfieldsUsed$ ) To 1 Step -1
                          TestChar$ = Mid$( SubfieldsUsed$, i, 1 )
                          If TestChar$ Like "[abdefgijpqrt]" Then
                              PrevSubfieldCode$ = TestChar$
                              ArrayIndex%       = ToggleCodeIndex( PrevSubfieldCode$ )
                          End If
                        Next i
                        CurrArrayString$ = Subfields( ArrayIndex% ).tInterLang
                        If InStr( CurrArrayString$, LangCode$ ) = 0 Then
                             Subfields( ArrayIndex% ).tInterLang = CurrArrayString$ & " " & LangCode$
                        End If
                      Else
                        For i = Len( SubfieldsUsed$ ) To 1 Step -1
                          TestChar$ = Mid$( SubfieldsUsed$, i, 1 )
                          If TestChar$ Like "[abdefghijkpqrt]" Then
                              PrevSubfieldCode$ = TestChar$
                              ArrayIndex%       = ToggleCodeIndex( PrevSubfieldCode$ )
                              Exit For
                          End If
                        Next i
                        CurrArrayString$ = Subfields( ArrayIndex% ).tOriginalLang
                        If InStr( CurrArrayString$, LangCode$ ) = 0 Then
                             Subfields( ArrayIndex% ).tOriginalLang = CurrArrayString$ & " " & LangCode$
                        End If
                        Subfields( ArrayIndex% ).tOriginalLangCode = SubfieldCode$
                    End If
                    CodeStringSplit% = CodeStringSplit% + LANG_CODE_LENGTH
                  Loop Until CodeStringSplit% > Len( SubfieldString$ )
                  If InStr( SubfieldsUsed$, SubfieldCode$ ) = 0 Then
                      SubfieldsUsed$ = SubfieldsUsed$ & SubfieldCode$
                  End If
              End If

          End If
          SubfieldSplit% = p + 1
        Loop Until p = 0

' Subfields $b and $f should have their contents sorted.

        TempString$ = Subfields( ToggleCodeIndex( "b" ) ).tPrimaryLangs
        If TempString$ <> "" Then
            Call SortSubfieldCodes( TempString$ )
        End If

        TempString$ = Subfields( ToggleCodeIndex( "f" ) ).tPrimaryLangs
        If TempString$ <> "" Then
            Call SortSubfieldCodes( TempString$ )
        End If

' Trim a leading space in each subfield, but make sure there is a trailing space, so the
' subfield is ready to add a new code.

        For j = 0 To 11
          TempString$ = LTrim$( Subfields( j ).tPrimaryLangs )
          If TempString$ <> "" Then
              If Right( TempString$, 1 ) <> " " Then
                  Subfields( j ).tPrimaryLangs = TempString$ & " "
              End If
          End If
          TempString$ = LTrim$( Subfields( j ).tInterLang )
          If TempString$ <> "" Then
              If Right( TempString$, 1 ) <> " " Then
                  Subfields( j ).tInterLang = TempString$ & " "
              End If
          End If
          TempString$ = LTrim$( Subfields( j ).tOriginalLang )
          If TempString$ <> "" Then
              If Right( TempString$, 1 ) <> " " Then
                  Subfields( j ).tOriginalLang = TempString$ & " "
              End If
          End If
        Next j

    End If
End If

If AddNew Then
    If FF_Lang$ <> "999" Then
        Subfields( TypeOfRecord% ).tPrimaryLangs     = FF_Lang$ & " "
        Subfields( TypeOfRecord% ).tOriginalLangCode = "h"
        SubfieldsUsed$ = ToggleCodeIndex( TypeOfRecord% )
        Tag$           = "041"
        Ind1$          = " "
        Ind2$          = " "
    End If
End If

' Add an arrow to subfields used in the list of subfields, solely as a visual aid.

For i = 0 To 11
  If Subfields( i ).tPrimaryLangs = "" Then
      SubfieldList( i ) = SPACE_PREFIX & SubfieldList( i )
    Else
      SubfieldList( i ) = ARROW_PREFIX & SubfieldList( i )
  End If
Next i

Begin Dialog Dialog1Definition  348, 274, WaltsMacros$, .Dialog1ControlFunction
  PushButton    214, 248,  64,  16, "",                                         .OK
  CancelButton  286, 248,  52,  16
  DropListBox    18,  22, 312, 112, SubfieldList(),                             .SubfieldsListBox
  TextBox        18,  40, 312,  12,                                             .PrimariesTextBox
  TextBox        18,  66, 128,  12,                                             .OriginalsTextBox
  TextBox       202,  66, 128,  12,                                             .IntermediatesTextBox
  DropListBox    18, 132, 144, 128, LanguageNames(),                            .LanguageSelectionListBox
  ListBox       186, 132, 144,  56, ShowCodesNames(),                           .ViewCodesListBox
  TextBox        18, 170,  88,  12,                                             .SearchBox
  PushButton    114, 170,  48,  12, "Search",                                   .SearchButton
  DropListBox   248, 188,  28,  48, RelatedSubfields(),                         .SubfieldTrioListBox
  PushButton    282, 188,  48,  12, "&Add",                                     .AddButton
  Text           18,  12, 312,   8, "",                                         .DialogBoxTopText
  Text           18,  56, 120,   8, "",                                         .OriginalLang
  Text          202,  56, 120,   8, "Intermediate translations [subfield $k]:", .InterLang
  Text           18,  83, 312,  16, DataEntryInstructions$,                     .DataEntryInstructions
  Text           18, 122, 144,   8, BrowseListInstructions$,                    .BrowseListInstructions
  Text          186, 122, 144,   8, "",                                         .ViewCodesLegend
  Text           18, 152, 144,  16, SearchBoxInstructions$,                     .SearchBoxInstructions
  Text          186, 190,  56,   8, "... to this subfield:",                    .AddCodePrompt
  Text           16, 223, 316,   8, "",                                         .DisplayField
  Text           16, 246, 128,  16, FFChangedNote$,                             .FFChangeNote
  GroupBox       10,   4, 328, 100, "",                                         .SubfieldsGroupBox
  GroupBox       10, 110, 160, 100, "Browse/Search languages",                  .CodesListGroupBox
  GroupBox      178, 110, 160, 100, "",                                         .ViewCodesGroupBox
  GroupBox       10, 214, 328,  24, "",                                         .DisplayFieldGroupBox
End Dialog

Dim Dialog1 As Dialog1Definition
On Error Resume Next
Dialog Dialog1
If Err = DIALOG_BUTTON_CANCEL Then Exit Sub

' Add a new field.

'If OriginalField$ <> "" Then
    If NewField$ <> OriginalField$ Then
        If AddNew = TRUE Then
            If CS.AddField( 1, NewField$ ) Then
                If Ind1$ = "1" And AddNew = TRUE Then
                    MsgBox "041 field added. Verify the value of the first indicator: Is the item really a translation?", WARNING_MESSAGE, WaltsMacros$
                End If
              Else
                MsgBox "Sorry, could not add new 041 field.", CRITICAL_MESSAGE, WaltsMacros$
            End If

' Replace an existing field.

          Else
            If In041 Then
                If CS.SetFieldLine( Row%, NewField$ ) = FALSE Then
                    MsgBox "Sorry, could not replace 041 field.", CRITICAL_MESSAGE, WaltsMacros$
                End If
              Else
                If CS.SetField( 1, NewField$ ) = FALSE Then
                    MsgBox "Sorry, could not replace 041 field.", CRITICAL_MESSAGE, WaltsMacros$
                End If
            End If
        End If
    End If
'End If

End Sub

'****************************************************************************************

Sub MatchNamesToCodes( StringIn$ )

' This sub takes an input of language codes and links them with their names, by using the
' two language arrays, in the form a list of items with the form "code = Language". This
' search must be a simple linear search through the whole list of language codes because
' that list is not in alphabetical order.

Const SUBSCRIPT_OUT_OF_RANGE As Integer = 9   'The Error value returned when an element of an array can't be accessed, as when
                                              ' a dynamic array has not been initialized.

Dim ArrayIndex%
Dim FieldNumber%
Dim LangCode$
Dim LangPair$
Dim UBShowCodesNames%
Dim WorkString$

Dim i As Integer, p As Integer'

Erase ShowCodesNames

WorkString$ = Trim$( StringIn$ )
If WorkString$ = "" Then
    Exit Sub
End If

' Remove any introduced double spaces, as this function uses the OML function "GetField,"
' in which a space character separates the "fields" (that is, codes) within a string.
' Extra spaces could prove fatal.

Do
  p = InStr( WorkString$, "  " )
  If p <> 0 Then
      WorkString$ = Left$( WorkString$, p - 1 ) & Mid$( WorkString$, p + 1 )
  End If
Loop Until p = 0

' Get all the three-character language codes.

ArrayIndex%  = 0
FieldNumber% = 1

Do
  LangCode$ = GetField$( WorkString$, FieldNumber%, " " )
  If LangCode$ = "" Then Exit Sub

' Check that the language code doesn't already appear in the array. If the array hasn't
' been initialized, do that now.

  On Error Resume Next
  UBShowCodesNames% = UBound( ShowCodesNames )
  If Err <> SUBSCRIPT_OUT_OF_RANGE Then
      For i = 0 To UBShowCodesNames%
        If LangCode$ = Trim$( Left$( ShowCodesNames( i ), 4 ) ) Then
            ArrayIndex% = ArrayIndex% + 1
            GoTo Increment:
        End If
      Next i
  End If

' Go through the array, from beginning to end, and get the name that matches the code,
' glue it to the code with the equals sign, and fill the intermediate array for later
' filling of the specific subfield array.

  If Len( LangCode$ ) > 2 Then
      If Len( LangCode$ ) = 3 Then
          For i = 1 To LANGUAGE_CODES_COUNT
            If LanguageCodes( i ) = LangCode$ Then
                LangPair$ = LangCode$ & " = " & LanguageNames( i )
                Exit For
            End If
          Next i
        ElseIf Len( LangCode$ ) > 3 Then
          LangPair$ = LangCode$ & " = ERROR IN CODE!"
      End If
    Else

' If codes are being deleted, remove them from the box as soon as they become strings of
' fewer than three characters.

      UBShowCodesNames% = UBound( ShowCodesNames )
      If ArrayIndex% = UBShowCodesNames% Then 'CODES ARE BEING DELETED?
          If ArrayIndex% = 0 then
              ReDim ShowCodesNames( 0 )
            Else
              ReDim Preserve ShowCodesNames( ArrayIndex% - 1 )
          End If
        ElseIf ArrayIndex% < UBShowCodesNames% Then
          For i = ArrayIndex% + 1 To UBShowCodesNames%
            ShowCodesNames( i - 1 ) = ShowCodesNames( i )
          Next i
          ReDim Preserve ShowCodesNames( UBShowCodesNames% - 1 )
      End If
      GoTo Increment:
  End If
 If LangPair$ = "" Then
      LangPair$ = LangCode$ & " = Unknown"
  End If
  ReDim Preserve ShowCodesNames( ArrayIndex% )
  ShowCodesNames( ArrayIndex% ) = LangPair$
  LangPair$   = ""
  ArrayIndex% = ArrayIndex% + 1

Increment:

  FieldNumber% = FieldNumber% + 1
Loop Until LangCode = ""

End Sub

'****************************************************************************************

Sub SearchLanguageNames( InString$ )

' This sub searches for language names that contain the search string and attempts to
' rank the results.

Dim LanguageName$
Dim RankPosition$
Dim RankSize$
Dim Term1$
Dim Term2$
Dim UBTempArray%

Dim Swapped

Dim i As Integer, j As Integer, p As Integer

Dim TempArray() As String

' A code that matches the search term is automatically added as the first entry in the
' list. Set a flag, because if this string is selected, it doesn't need to have its code
' searched as all the other results in the TempArray do.

ReDim TempArray( 0 )
For i = 1 To LANGUAGE_CODES_COUNT
  If InString$ = LanguageCodes( i ) Then
      TempArray( 0 )    = "000000" & InString$ & " = " & LanguageNames( i )
      Exit For
  End If
Next i

If TempArray( 0 ) = "" Then
    j = 0
  Else
    j = 1
End If

' Then go through the list of language names, from beginning to end, looking for a match
' to the search term. From each language name, remove parenthetical information and the
' words "language" and "languages," as they should not be considered for the comparison.

For i = 1 To LANGUAGE_CODES_COUNT
  LanguageName$ = LanguageNames( i )
  p = InStr( LanguageName$, "(" )
  If p > 0 Then
      LanguageName$ = Trim$( Left$( LanguageName$, p - 1 ) )
  End If
  p = InStr( LanguageName$, "languages" )
  If p > 0 Then
      LanguageName$ = Trim$( Left$( LanguageName$, p - 1 ) )
  End If
  p = InStr( LanguageName$, InString$ )

' Ranking the results takes into account two factors: 1) How close the search string is
' to the beginning of the language name--the assumption being that the desired name is
' more likely to be searched by its beginning than by a sequence of characters in its
' middle; and 2) How much of the desired name is matched by the search string--the
' assumption being that the more complete the match, the more likely it is that that name
' is the one being sought. The two ranks are turned into number strings and prefixed to
' the language names. The strings will be sorted, then the prefixed numbers removed for
' display.

  If p > 0 Then

' The pseudo-scientific ranking system. The "rank position" factor takes the position of
' the search string within the language name and multiplies that number by 5.

      RankPosition$ = Trim$( Str$( ( p - 1 ) * 5 ) )
      RankPosition$ = String$( 3 - Len( RankPosition$ ), "0" ) & RankPosition$

' The "rank size" factor calculates the ratio of the length of the language name to the
' length of the search string, inverts it, multiplies it by 10, and makes it an integer.

      p = Int( 10 / ( Len( InString$ ) / Len( LanguageName$ ) ) )
      RankSize$ = Trim$( Str$( p ) )
      RankSize$ = String$( 3 - Len( RankSize$ ), "0" ) & RankSize$
      ReDim Preserve TempArray( j )
      TempArray( j ) = RankPosition$ & RankSize$ & LanguageNames( i )
      j = j + 1
  End If
Next i

UBTempArray% = UBound( TempArray )

' Do a bubble sort to get the strings in order of relevance.

Do
  Swapped = FALSE
  For i = 0 To UBTempArray% - 1
    Term1$ = TempArray( i )
    Term2$ = TempArray( i + 1 )
    If Term1$ > Term2$ Then
        TempArray( i )     = Term2$
        TempArray( i + 1 ) = Term1$
        Swapped            = TRUE
    End If
  Next i
Loop Until Swapped = FALSE

If UBTempArray% = 0 And TempArray( 0 ) = "" Then
    UBTempArray% = 1
    ReDim TempArray( UBTempArray% )
    TempArray( 0 ) = "      No match was found for the search term"
    TempArray( 1 ) = "      " & DOUBLE_QUOTE & InString$ & DOUBLE_QUOTE
End If

ReDim ShowCodesNames( UBTempArray% )
For i = 0 To UBTempArray%
  ShowCodesNames( i ) = Mid$( TempArray( i ), 7 )
Next i

End Sub

'****************************************************************************************

Sub SetLanguageArrays

' This routine fills the arrays for help in adding language codes. Codes and names are
' from MARC List for Languages at <https://id.loc.gov/vocabulary/languages.html>, last
' updated 2012-08-02. Discontinued codes, and languages assigned a group code only, are
' excluded, as also are tracings or references.

LanguageCodes( 0   ) = ""    : LanguageNames( 0   ) = "  - - - Select a language - - -  "
LanguageCodes( 1   ) = "abk" : LanguageNames( 1   ) = "Abkhaz"
LanguageCodes( 2   ) = "ace" : LanguageNames( 2   ) = "Achinese"
LanguageCodes( 3   ) = "ach" : LanguageNames( 3   ) = "Acoli"
LanguageCodes( 4   ) = "ada" : LanguageNames( 4   ) = "Adangme"
LanguageCodes( 5   ) = "ady" : LanguageNames( 5   ) = "Adygei"
LanguageCodes( 6   ) = "aar" : LanguageNames( 6   ) = "Afar"
LanguageCodes( 7   ) = "afh" : LanguageNames( 7   ) = "Afrihili (Artificial language)"
LanguageCodes( 8   ) = "afr" : LanguageNames( 8   ) = "Afrikaans"
LanguageCodes( 9   ) = "afa" : LanguageNames( 9   ) = "Afroasiatic (Other)"
LanguageCodes( 10  ) = "ain" : LanguageNames( 10  ) = "Ainu"
LanguageCodes( 11  ) = "aka" : LanguageNames( 11  ) = "Akan"
LanguageCodes( 12  ) = "akk" : LanguageNames( 12  ) = "Akkadian"
LanguageCodes( 13  ) = "alb" : LanguageNames( 13  ) = "Albanian"
LanguageCodes( 14  ) = "ale" : LanguageNames( 14  ) = "Aleut"
LanguageCodes( 15  ) = "alg" : LanguageNames( 15  ) = "Algonquian (Other)"
LanguageCodes( 16  ) = "alt" : LanguageNames( 16  ) = "Altai"
LanguageCodes( 17  ) = "tut" : LanguageNames( 17  ) = "Altaic (Other)"
LanguageCodes( 18  ) = "amh" : LanguageNames( 18  ) = "Amharic"
LanguageCodes( 19  ) = "anp" : LanguageNames( 19  ) = "Angika"
LanguageCodes( 20  ) = "apa" : LanguageNames( 20  ) = "Apache languages"
LanguageCodes( 21  ) = "ara" : LanguageNames( 21  ) = "Arabic"
LanguageCodes( 22  ) = "arg" : LanguageNames( 22  ) = "Aragonese"
LanguageCodes( 23  ) = "arc" : LanguageNames( 23  ) = "Aramaic"
LanguageCodes( 24  ) = "arp" : LanguageNames( 24  ) = "Arapaho"
LanguageCodes( 25  ) = "arw" : LanguageNames( 25  ) = "Arawak"
LanguageCodes( 26  ) = "arm" : LanguageNames( 26  ) = "Armenian"
LanguageCodes( 27  ) = "rup" : LanguageNames( 27  ) = "Aromanian"
LanguageCodes( 28  ) = "art" : LanguageNames( 28  ) = "Artificial (Other)"
LanguageCodes( 29  ) = "asm" : LanguageNames( 29  ) = "Assamese"
LanguageCodes( 30  ) = "ath" : LanguageNames( 30  ) = "Athapascan (Other)"
LanguageCodes( 31  ) = "aus" : LanguageNames( 31  ) = "Australian languages"
LanguageCodes( 32  ) = "map" : LanguageNames( 32  ) = "Austronesian (Other)"
LanguageCodes( 33  ) = "ava" : LanguageNames( 33  ) = "Avaric"
LanguageCodes( 34  ) = "ave" : LanguageNames( 34  ) = "Avestan"
LanguageCodes( 35  ) = "awa" : LanguageNames( 35  ) = "Awadhi"
LanguageCodes( 36  ) = "aym" : LanguageNames( 36  ) = "Aymara"
LanguageCodes( 37  ) = "aze" : LanguageNames( 37  ) = "Azerbaijani"
LanguageCodes( 38  ) = "ast" : LanguageNames( 38  ) = "Bable"
LanguageCodes( 39  ) = "ban" : LanguageNames( 39  ) = "Balinese"
LanguageCodes( 40  ) = "bat" : LanguageNames( 40  ) = "Baltic (Other)"
LanguageCodes( 41  ) = "bal" : LanguageNames( 41  ) = "Baluchi"
LanguageCodes( 42  ) = "bam" : LanguageNames( 42  ) = "Bambara"
LanguageCodes( 43  ) = "bai" : LanguageNames( 43  ) = "Bamileke languages"
LanguageCodes( 44  ) = "bad" : LanguageNames( 44  ) = "Banda languages"
LanguageCodes( 45  ) = "bnt" : LanguageNames( 45  ) = "Bantu (Other)"
LanguageCodes( 46  ) = "bas" : LanguageNames( 46  ) = "Basa"
LanguageCodes( 47  ) = "bak" : LanguageNames( 47  ) = "Bashkir"
LanguageCodes( 48  ) = "baq" : LanguageNames( 48  ) = "Basque"
LanguageCodes( 49  ) = "btk" : LanguageNames( 49  ) = "Batak"
LanguageCodes( 50  ) = "bej" : LanguageNames( 50  ) = "Beja"
LanguageCodes( 51  ) = "bel" : LanguageNames( 51  ) = "Belarusian"
LanguageCodes( 52  ) = "bem" : LanguageNames( 52  ) = "Bemba"
LanguageCodes( 53  ) = "ben" : LanguageNames( 53  ) = "Bengali"
LanguageCodes( 54  ) = "ber" : LanguageNames( 54  ) = "Berber (Other)"
LanguageCodes( 55  ) = "bho" : LanguageNames( 55  ) = "Bhojpuri"
LanguageCodes( 56  ) = "bih" : LanguageNames( 56  ) = "Bihari (Other)"
LanguageCodes( 57  ) = "bik" : LanguageNames( 57  ) = "Bikol"
LanguageCodes( 58  ) = "byn" : LanguageNames( 58  ) = "Bilin"
LanguageCodes( 59  ) = "bis" : LanguageNames( 59  ) = "Bislama"
LanguageCodes( 60  ) = "zbl" : LanguageNames( 60  ) = "Blissymbolics"
LanguageCodes( 61  ) = "bos" : LanguageNames( 61  ) = "Bosnian"
LanguageCodes( 62  ) = "bra" : LanguageNames( 62  ) = "Braj"
LanguageCodes( 63  ) = "bre" : LanguageNames( 63  ) = "Breton"
LanguageCodes( 64  ) = "bug" : LanguageNames( 64  ) = "Bugis"
LanguageCodes( 65  ) = "bul" : LanguageNames( 65  ) = "Bulgarian"
LanguageCodes( 66  ) = "bua" : LanguageNames( 66  ) = "Buriat"
LanguageCodes( 67  ) = "bur" : LanguageNames( 67  ) = "Burmese"
LanguageCodes( 68  ) = "cad" : LanguageNames( 68  ) = "Caddo"
LanguageCodes( 69  ) = "car" : LanguageNames( 69  ) = "Carib"
LanguageCodes( 70  ) = "cat" : LanguageNames( 70  ) = "Catalan"
LanguageCodes( 71  ) = "cau" : LanguageNames( 71  ) = "Caucasian (Other)"
LanguageCodes( 72  ) = "ceb" : LanguageNames( 72  ) = "Cebuano"
LanguageCodes( 73  ) = "cel" : LanguageNames( 73  ) = "Celtic (Other)"
LanguageCodes( 74  ) = "cai" : LanguageNames( 74  ) = "Central American Indian (Other)"
LanguageCodes( 75  ) = "chg" : LanguageNames( 75  ) = "Chagatai"
LanguageCodes( 76  ) = "cmc" : LanguageNames( 76  ) = "Chamic languages"
LanguageCodes( 77  ) = "cha" : LanguageNames( 77  ) = "Chamorro"
LanguageCodes( 78  ) = "che" : LanguageNames( 78  ) = "Chechen"
LanguageCodes( 79  ) = "chr" : LanguageNames( 79  ) = "Cherokee"
LanguageCodes( 80  ) = "chy" : LanguageNames( 80  ) = "Cheyenne"
LanguageCodes( 81  ) = "chb" : LanguageNames( 81  ) = "Chibcha"
LanguageCodes( 82  ) = "chi" : LanguageNames( 82  ) = "Chinese"
LanguageCodes( 83  ) = "chn" : LanguageNames( 83  ) = "Chinook jargon"
LanguageCodes( 84  ) = "chp" : LanguageNames( 84  ) = "Chipewyan"
LanguageCodes( 85  ) = "cho" : LanguageNames( 85  ) = "Choctaw"
LanguageCodes( 86  ) = "chu" : LanguageNames( 86  ) = "Church Slavic"
LanguageCodes( 87  ) = "chk" : LanguageNames( 87  ) = "Chuukese"
LanguageCodes( 88  ) = "chv" : LanguageNames( 88  ) = "Chuvash"
LanguageCodes( 89  ) = "cop" : LanguageNames( 89  ) = "Coptic"
LanguageCodes( 90  ) = "cor" : LanguageNames( 90  ) = "Cornish"
LanguageCodes( 91  ) = "cos" : LanguageNames( 91  ) = "Corsican"
LanguageCodes( 92  ) = "cre" : LanguageNames( 92  ) = "Cree"
LanguageCodes( 93  ) = "mus" : LanguageNames( 93  ) = "Creek"
LanguageCodes( 94  ) = "crp" : LanguageNames( 94  ) = "Creoles and Pidgins (Other)"
LanguageCodes( 95  ) = "cpe" : LanguageNames( 95  ) = "Creoles and Pidgins, English-based (Other)"
LanguageCodes( 96  ) = "cpf" : LanguageNames( 96  ) = "Creoles and Pidgins, French-based (Other)"
LanguageCodes( 97  ) = "cpp" : LanguageNames( 97  ) = "Creoles and Pidgins, Portuguese-based (Other)"
LanguageCodes( 98  ) = "crh" : LanguageNames( 98  ) = "Crimean Tatar"
LanguageCodes( 99  ) = "hrv" : LanguageNames( 99  ) = "Croatian"
LanguageCodes( 100 ) = "cus" : LanguageNames( 100 ) = "Cushitic (Other)"
LanguageCodes( 101 ) = "cze" : LanguageNames( 101 ) = "Czech"
LanguageCodes( 102 ) = "dak" : LanguageNames( 102 ) = "Dakota"
LanguageCodes( 103 ) = "dan" : LanguageNames( 103 ) = "Danish"
LanguageCodes( 104 ) = "dar" : LanguageNames( 104 ) = "Dargwa"
LanguageCodes( 105 ) = "day" : LanguageNames( 105 ) = "Dayak"
LanguageCodes( 106 ) = "del" : LanguageNames( 106 ) = "Delaware"
LanguageCodes( 107 ) = "din" : LanguageNames( 107 ) = "Dinka"
LanguageCodes( 108 ) = "div" : LanguageNames( 108 ) = "Divehi"
LanguageCodes( 109 ) = "doi" : LanguageNames( 109 ) = "Dogri"
LanguageCodes( 110 ) = "dgr" : LanguageNames( 110 ) = "Dogrib"
LanguageCodes( 111 ) = "dra" : LanguageNames( 111 ) = "Dravidian (Other)"
LanguageCodes( 112 ) = "dua" : LanguageNames( 112 ) = "Duala"
LanguageCodes( 113 ) = "dut" : LanguageNames( 113 ) = "Dutch"
LanguageCodes( 114 ) = "dum" : LanguageNames( 114 ) = "Dutch, Middle (ca. 1050-1350)"
LanguageCodes( 115 ) = "dyu" : LanguageNames( 115 ) = "Dyula"
LanguageCodes( 116 ) = "dzo" : LanguageNames( 116 ) = "Dzongkha"
LanguageCodes( 117 ) = "frs" : LanguageNames( 117 ) = "East Frisian"
LanguageCodes( 118 ) = "bin" : LanguageNames( 118 ) = "Edo"
LanguageCodes( 119 ) = "efi" : LanguageNames( 119 ) = "Efik"
LanguageCodes( 120 ) = "egy" : LanguageNames( 120 ) = "Egyptian"
LanguageCodes( 121 ) = "eka" : LanguageNames( 121 ) = "Ekajuk"
LanguageCodes( 122 ) = "elx" : LanguageNames( 122 ) = "Elamite"
LanguageCodes( 123 ) = "eng" : LanguageNames( 123 ) = "English"
LanguageCodes( 124 ) = "enm" : LanguageNames( 124 ) = "English, Middle (1100-1500)"
LanguageCodes( 125 ) = "ang" : LanguageNames( 125 ) = "English, Old (ca. 450-1100)"
LanguageCodes( 126 ) = "myv" : LanguageNames( 126 ) = "Erzya"
LanguageCodes( 127 ) = "epo" : LanguageNames( 127 ) = "Esperanto"
LanguageCodes( 128 ) = "est" : LanguageNames( 128 ) = "Estonian"
LanguageCodes( 129 ) = "gez" : LanguageNames( 129 ) = "Ethiopic"
LanguageCodes( 130 ) = "ewe" : LanguageNames( 130 ) = "Ewe"
LanguageCodes( 131 ) = "ewo" : LanguageNames( 131 ) = "Ewondo"
LanguageCodes( 132 ) = "fan" : LanguageNames( 132 ) = "Fang"
LanguageCodes( 133 ) = "fat" : LanguageNames( 133 ) = "Fanti"
LanguageCodes( 134 ) = "fao" : LanguageNames( 134 ) = "Faroese"
LanguageCodes( 135 ) = "fij" : LanguageNames( 135 ) = "Fijian"
LanguageCodes( 136 ) = "fil" : LanguageNames( 136 ) = "Filipino"
LanguageCodes( 137 ) = "fin" : LanguageNames( 137 ) = "Finnish"
LanguageCodes( 138 ) = "fiu" : LanguageNames( 138 ) = "Finno-Ugrian (Other)"
LanguageCodes( 139 ) = "fon" : LanguageNames( 139 ) = "Fon"
LanguageCodes( 140 ) = "fre" : LanguageNames( 140 ) = "French"
LanguageCodes( 141 ) = "frm" : LanguageNames( 141 ) = "French, Middle (ca. 1300-1600)"
LanguageCodes( 142 ) = "fro" : LanguageNames( 142 ) = "French, Old (ca. 842-1300)"
LanguageCodes( 143 ) = "fry" : LanguageNames( 143 ) = "Frisian"
LanguageCodes( 144 ) = "fur" : LanguageNames( 144 ) = "Friulian"
LanguageCodes( 145 ) = "ful" : LanguageNames( 145 ) = "Fula"
LanguageCodes( 146 ) = "glg" : LanguageNames( 146 ) = "Galician"
LanguageCodes( 147 ) = "lug" : LanguageNames( 147 ) = "Ganda"
LanguageCodes( 148 ) = "gay" : LanguageNames( 148 ) = "Gayo"
LanguageCodes( 149 ) = "gba" : LanguageNames( 149 ) = "Gbaya"
LanguageCodes( 150 ) = "geo" : LanguageNames( 150 ) = "Georgian"
LanguageCodes( 151 ) = "ger" : LanguageNames( 151 ) = "German"
LanguageCodes( 152 ) = "gmh" : LanguageNames( 152 ) = "German, Middle High (ca. 1050-1500)"
LanguageCodes( 153 ) = "goh" : LanguageNames( 153 ) = "German, Old High (ca. 750-1050)"
LanguageCodes( 154 ) = "gem" : LanguageNames( 154 ) = "Germanic (Other)"
LanguageCodes( 155 ) = "gil" : LanguageNames( 155 ) = "Gilbertese"
LanguageCodes( 156 ) = "gon" : LanguageNames( 156 ) = "Gondi"
LanguageCodes( 157 ) = "gor" : LanguageNames( 157 ) = "Gorontalo"
LanguageCodes( 158 ) = "got" : LanguageNames( 158 ) = "Gothic"
LanguageCodes( 159 ) = "grb" : LanguageNames( 159 ) = "Grebo"
LanguageCodes( 160 ) = "grc" : LanguageNames( 160 ) = "Greek, Ancient (to 1453)"
LanguageCodes( 161 ) = "gre" : LanguageNames( 161 ) = "Greek, Modern (1453- )"
LanguageCodes( 162 ) = "grn" : LanguageNames( 162 ) = "Guarani"
LanguageCodes( 163 ) = "guj" : LanguageNames( 163 ) = "Gujarati"
LanguageCodes( 164 ) = "gwi" : LanguageNames( 164 ) = "Gwich'in"
LanguageCodes( 165 ) = "gaa" : LanguageNames( 165 ) = "Gã"
LanguageCodes( 166 ) = "hai" : LanguageNames( 166 ) = "Haida"
LanguageCodes( 167 ) = "hat" : LanguageNames( 167 ) = "Haitian French Creole"
LanguageCodes( 168 ) = "hau" : LanguageNames( 168 ) = "Hausa"
LanguageCodes( 169 ) = "haw" : LanguageNames( 169 ) = "Hawaiian"
LanguageCodes( 170 ) = "heb" : LanguageNames( 170 ) = "Hebrew"
LanguageCodes( 171 ) = "her" : LanguageNames( 171 ) = "Herero"
LanguageCodes( 172 ) = "hil" : LanguageNames( 172 ) = "Hiligaynon"
LanguageCodes( 173 ) = "hin" : LanguageNames( 173 ) = "Hindi"
LanguageCodes( 174 ) = "hmo" : LanguageNames( 174 ) = "Hiri Motu"
LanguageCodes( 175 ) = "hit" : LanguageNames( 175 ) = "Hittite"
LanguageCodes( 176 ) = "hmn" : LanguageNames( 176 ) = "Hmong"
LanguageCodes( 177 ) = "hun" : LanguageNames( 177 ) = "Hungarian"
LanguageCodes( 178 ) = "hup" : LanguageNames( 178 ) = "Hupa"
LanguageCodes( 179 ) = "iba" : LanguageNames( 179 ) = "Iban"
LanguageCodes( 180 ) = "ice" : LanguageNames( 180 ) = "Icelandic"
LanguageCodes( 181 ) = "ido" : LanguageNames( 181 ) = "Ido"
LanguageCodes( 182 ) = "ibo" : LanguageNames( 182 ) = "Igbo"
LanguageCodes( 183 ) = "ijo" : LanguageNames( 183 ) = "Ijo"
LanguageCodes( 184 ) = "ilo" : LanguageNames( 184 ) = "Iloko"
LanguageCodes( 185 ) = "smn" : LanguageNames( 185 ) = "Inari Sami"
LanguageCodes( 186 ) = "inc" : LanguageNames( 186 ) = "Indic (Other)"
LanguageCodes( 187 ) = "ine" : LanguageNames( 187 ) = "Indo-European (Other)"
LanguageCodes( 188 ) = "ind" : LanguageNames( 188 ) = "Indonesian"
LanguageCodes( 189 ) = "inh" : LanguageNames( 189 ) = "Ingush"
LanguageCodes( 190 ) = "ina" : LanguageNames( 190 ) = "Interlingua (International Auxiliary Language Association)"
LanguageCodes( 191 ) = "ile" : LanguageNames( 191 ) = "Interlingue"
LanguageCodes( 192 ) = "iku" : LanguageNames( 192 ) = "Inuktitut"
LanguageCodes( 193 ) = "ipk" : LanguageNames( 193 ) = "Inupiaq"
LanguageCodes( 194 ) = "ira" : LanguageNames( 194 ) = "Iranian (Other)"
LanguageCodes( 195 ) = "gle" : LanguageNames( 195 ) = "Irish"
LanguageCodes( 196 ) = "mga" : LanguageNames( 196 ) = "Irish, Middle (ca. 1100-1550)"
LanguageCodes( 197 ) = "sga" : LanguageNames( 197 ) = "Irish, Old (to 1100)"
LanguageCodes( 198 ) = "iro" : LanguageNames( 198 ) = "Iroquoian (Other)"
LanguageCodes( 199 ) = "ita" : LanguageNames( 199 ) = "Italian"
LanguageCodes( 200 ) = "jpn" : LanguageNames( 200 ) = "Japanese"
LanguageCodes( 201 ) = "jav" : LanguageNames( 201 ) = "Javanese"
LanguageCodes( 202 ) = "jrb" : LanguageNames( 202 ) = "Judeo-Arabic"
LanguageCodes( 203 ) = "jpr" : LanguageNames( 203 ) = "Judeo-Persian"
LanguageCodes( 204 ) = "kbd" : LanguageNames( 204 ) = "Kabardian"
LanguageCodes( 205 ) = "kab" : LanguageNames( 205 ) = "Kabyle"
LanguageCodes( 206 ) = "kac" : LanguageNames( 206 ) = "Kachin"
LanguageCodes( 207 ) = "kal" : LanguageNames( 207 ) = "Kalâtdlisut"
LanguageCodes( 208 ) = "kam" : LanguageNames( 208 ) = "Kamba"
LanguageCodes( 209 ) = "kan" : LanguageNames( 209 ) = "Kannada"
LanguageCodes( 210 ) = "kau" : LanguageNames( 210 ) = "Kanuri"
LanguageCodes( 211 ) = "kaa" : LanguageNames( 211 ) = "Kara-Kalpak"
LanguageCodes( 212 ) = "krc" : LanguageNames( 212 ) = "Karachay-Balkar"
LanguageCodes( 213 ) = "krl" : LanguageNames( 213 ) = "Karelian"
LanguageCodes( 214 ) = "kar" : LanguageNames( 214 ) = "Karen languages"
LanguageCodes( 215 ) = "kas" : LanguageNames( 215 ) = "Kashmiri"
LanguageCodes( 216 ) = "csb" : LanguageNames( 216 ) = "Kashubian"
LanguageCodes( 217 ) = "kaw" : LanguageNames( 217 ) = "Kawi"
LanguageCodes( 218 ) = "kaz" : LanguageNames( 218 ) = "Kazakh"
LanguageCodes( 219 ) = "kha" : LanguageNames( 219 ) = "Khasi"
LanguageCodes( 220 ) = "khm" : LanguageNames( 220 ) = "Khmer"
LanguageCodes( 221 ) = "khi" : LanguageNames( 221 ) = "Khoisan (Other)"
LanguageCodes( 222 ) = "kho" : LanguageNames( 222 ) = "Khotanese"
LanguageCodes( 223 ) = "kik" : LanguageNames( 223 ) = "Kikuyu"
LanguageCodes( 224 ) = "kmb" : LanguageNames( 224 ) = "Kimbundu"
LanguageCodes( 225 ) = "kin" : LanguageNames( 225 ) = "Kinyarwanda"
LanguageCodes( 226 ) = "tlh" : LanguageNames( 226 ) = "Klingon (Artificial language)"
LanguageCodes( 227 ) = "kom" : LanguageNames( 227 ) = "Komi"
LanguageCodes( 228 ) = "kon" : LanguageNames( 228 ) = "Kongo"
LanguageCodes( 229 ) = "kok" : LanguageNames( 229 ) = "Konkani"
LanguageCodes( 230 ) = "kut" : LanguageNames( 230 ) = "Kootenai"
LanguageCodes( 231 ) = "kor" : LanguageNames( 231 ) = "Korean"
LanguageCodes( 232 ) = "kos" : LanguageNames( 232 ) = "Kosraean"
LanguageCodes( 233 ) = "kpe" : LanguageNames( 233 ) = "Kpelle"
LanguageCodes( 234 ) = "kro" : LanguageNames( 234 ) = "Kru (Other)"
LanguageCodes( 235 ) = "kua" : LanguageNames( 235 ) = "Kuanyama"
LanguageCodes( 236 ) = "kum" : LanguageNames( 236 ) = "Kumyk"
LanguageCodes( 237 ) = "kur" : LanguageNames( 237 ) = "Kurdish"
LanguageCodes( 238 ) = "kru" : LanguageNames( 238 ) = "Kurukh"
LanguageCodes( 239 ) = "kir" : LanguageNames( 239 ) = "Kyrgyz"
LanguageCodes( 240 ) = "lad" : LanguageNames( 240 ) = "Ladino"
LanguageCodes( 241 ) = "lah" : LanguageNames( 241 ) = "Lahnda"
LanguageCodes( 242 ) = "lam" : LanguageNames( 242 ) = "Lamba (Zambia and Congo)"
LanguageCodes( 243 ) = "lao" : LanguageNames( 243 ) = "Lao"
LanguageCodes( 244 ) = "lat" : LanguageNames( 244 ) = "Latin"
LanguageCodes( 245 ) = "lav" : LanguageNames( 245 ) = "Latvian"
LanguageCodes( 246 ) = "lez" : LanguageNames( 246 ) = "Lezgian"
LanguageCodes( 247 ) = "lim" : LanguageNames( 247 ) = "Limburgish"
LanguageCodes( 248 ) = "lin" : LanguageNames( 248 ) = "Lingala"
LanguageCodes( 249 ) = "lit" : LanguageNames( 249 ) = "Lithuanian"
LanguageCodes( 250 ) = "jbo" : LanguageNames( 250 ) = "Lojban (Artificial language)"
LanguageCodes( 251 ) = "nds" : LanguageNames( 251 ) = "Low German"
LanguageCodes( 252 ) = "dsb" : LanguageNames( 252 ) = "Lower Sorbian"
LanguageCodes( 253 ) = "loz" : LanguageNames( 253 ) = "Lozi"
LanguageCodes( 254 ) = "lub" : LanguageNames( 254 ) = "Luba-Katanga"
LanguageCodes( 255 ) = "lua" : LanguageNames( 255 ) = "Luba-Lulua"
LanguageCodes( 256 ) = "lui" : LanguageNames( 256 ) = "Luiseño"
LanguageCodes( 257 ) = "smj" : LanguageNames( 257 ) = "Lule Sami"
LanguageCodes( 258 ) = "lun" : LanguageNames( 258 ) = "Lunda"
LanguageCodes( 259 ) = "luo" : LanguageNames( 259 ) = "Luo (Kenya and Tanzania)"
LanguageCodes( 260 ) = "lus" : LanguageNames( 260 ) = "Lushai"
LanguageCodes( 261 ) = "ltz" : LanguageNames( 261 ) = "Luxembourgish"
LanguageCodes( 262 ) = "mas" : LanguageNames( 262 ) = "Maasai"
LanguageCodes( 263 ) = "mac" : LanguageNames( 263 ) = "Macedonian"
LanguageCodes( 264 ) = "mad" : LanguageNames( 264 ) = "Madurese"
LanguageCodes( 265 ) = "mag" : LanguageNames( 265 ) = "Magahi"
LanguageCodes( 266 ) = "mai" : LanguageNames( 266 ) = "Maithili"
LanguageCodes( 267 ) = "mak" : LanguageNames( 267 ) = "Makasar"
LanguageCodes( 268 ) = "mlg" : LanguageNames( 268 ) = "Malagasy"
LanguageCodes( 269 ) = "may" : LanguageNames( 269 ) = "Malay"
LanguageCodes( 270 ) = "mal" : LanguageNames( 270 ) = "Malayalam"
LanguageCodes( 271 ) = "mlt" : LanguageNames( 271 ) = "Maltese"
LanguageCodes( 272 ) = "mnc" : LanguageNames( 272 ) = "Manchu"
LanguageCodes( 273 ) = "mdr" : LanguageNames( 273 ) = "Mandar"
LanguageCodes( 274 ) = "man" : LanguageNames( 274 ) = "Mandingo"
LanguageCodes( 275 ) = "mni" : LanguageNames( 275 ) = "Manipuri"
LanguageCodes( 276 ) = "mno" : LanguageNames( 276 ) = "Manobo languages"
LanguageCodes( 277 ) = "glv" : LanguageNames( 277 ) = "Manx"
LanguageCodes( 278 ) = "mao" : LanguageNames( 278 ) = "Maori"
LanguageCodes( 279 ) = "arn" : LanguageNames( 279 ) = "Mapuche"
LanguageCodes( 280 ) = "mar" : LanguageNames( 280 ) = "Marathi"
LanguageCodes( 281 ) = "chm" : LanguageNames( 281 ) = "Mari"
LanguageCodes( 282 ) = "mah" : LanguageNames( 282 ) = "Marshallese"
LanguageCodes( 283 ) = "mwr" : LanguageNames( 283 ) = "Marwari"
LanguageCodes( 284 ) = "myn" : LanguageNames( 284 ) = "Mayan languages"
LanguageCodes( 285 ) = "men" : LanguageNames( 285 ) = "Mende"
LanguageCodes( 286 ) = "mic" : LanguageNames( 286 ) = "Micmac"
LanguageCodes( 287 ) = "min" : LanguageNames( 287 ) = "Minangkabau"
LanguageCodes( 288 ) = "mwl" : LanguageNames( 288 ) = "Mirandese"
LanguageCodes( 289 ) = "mis" : LanguageNames( 289 ) = "Miscellaneous languages"
LanguageCodes( 290 ) = "moh" : LanguageNames( 290 ) = "Mohawk"
LanguageCodes( 291 ) = "mdf" : LanguageNames( 291 ) = "Moksha"
LanguageCodes( 292 ) = "mkh" : LanguageNames( 292 ) = "Mon-Khmer (Other)"
LanguageCodes( 293 ) = "lol" : LanguageNames( 293 ) = "Mongo-Nkundu"
LanguageCodes( 294 ) = "mon" : LanguageNames( 294 ) = "Mongolian"
LanguageCodes( 295 ) = "cnr" : LanguageNames( 295 ) = "Montenegrin"
LanguageCodes( 296 ) = "mos" : LanguageNames( 296 ) = "Mooré"
LanguageCodes( 297 ) = "mul" : LanguageNames( 297 ) = "Multiple languages"
LanguageCodes( 298 ) = "mun" : LanguageNames( 298 ) = "Munda (Other)"
LanguageCodes( 299 ) = "nqo" : LanguageNames( 299 ) = "N'Ko"
LanguageCodes( 300 ) = "nah" : LanguageNames( 300 ) = "Nahuatl"
LanguageCodes( 301 ) = "nau" : LanguageNames( 301 ) = "Nauru"
LanguageCodes( 302 ) = "nav" : LanguageNames( 302 ) = "Navajo"
LanguageCodes( 303 ) = "nbl" : LanguageNames( 303 ) = "Ndebele (South Africa)"
LanguageCodes( 304 ) = "nde" : LanguageNames( 304 ) = "Ndebele (Zimbabwe)"
LanguageCodes( 305 ) = "ndo" : LanguageNames( 305 ) = "Ndonga"
LanguageCodes( 306 ) = "nap" : LanguageNames( 306 ) = "Neapolitan Italian"
LanguageCodes( 307 ) = "nep" : LanguageNames( 307 ) = "Nepali"
LanguageCodes( 308 ) = "new" : LanguageNames( 308 ) = "Newari"
LanguageCodes( 309 ) = "nwc" : LanguageNames( 309 ) = "Newari, Old"
LanguageCodes( 310 ) = "nia" : LanguageNames( 310 ) = "Nias"
LanguageCodes( 311 ) = "nic" : LanguageNames( 311 ) = "Niger-Kordofanian (Other)"
LanguageCodes( 312 ) = "ssa" : LanguageNames( 312 ) = "Nilo-Saharan (Other)"
LanguageCodes( 313 ) = "niu" : LanguageNames( 313 ) = "Niuean"
LanguageCodes( 314 ) = "zxx" : LanguageNames( 314 ) = "No linguistic content"
LanguageCodes( 315 ) = "nog" : LanguageNames( 315 ) = "Nogai"
LanguageCodes( 316 ) = "nai" : LanguageNames( 316 ) = "North American Indian (Other)"
LanguageCodes( 317 ) = "frr" : LanguageNames( 317 ) = "North Frisian"
LanguageCodes( 318 ) = "sme" : LanguageNames( 318 ) = "Northern Sami"
LanguageCodes( 319 ) = "nso" : LanguageNames( 319 ) = "Northern Sotho"
LanguageCodes( 320 ) = "nob" : LanguageNames( 320 ) = "Norwegian (Bokmål)"
LanguageCodes( 321 ) = "nno" : LanguageNames( 321 ) = "Norwegian (Nynorsk)"
LanguageCodes( 322 ) = "nor" : LanguageNames( 322 ) = "Norwegian"
LanguageCodes( 323 ) = "nub" : LanguageNames( 323 ) = "Nubian languages"
LanguageCodes( 324 ) = "nym" : LanguageNames( 324 ) = "Nyamwezi"
LanguageCodes( 325 ) = "nya" : LanguageNames( 325 ) = "Nyanja"
LanguageCodes( 326 ) = "nyn" : LanguageNames( 326 ) = "Nyankole"
LanguageCodes( 327 ) = "nyo" : LanguageNames( 327 ) = "Nyoro"
LanguageCodes( 328 ) = "nzi" : LanguageNames( 328 ) = "Nzima"
LanguageCodes( 329 ) = "oci" : LanguageNames( 329 ) = "Occitan (post-1500)"
LanguageCodes( 330 ) = "xal" : LanguageNames( 330 ) = "Oirat"
LanguageCodes( 331 ) = "oji" : LanguageNames( 331 ) = "Ojibwa"
LanguageCodes( 332 ) = "non" : LanguageNames( 332 ) = "Old Norse"
LanguageCodes( 333 ) = "peo" : LanguageNames( 333 ) = "Old Persian (ca. 600-400 B.C.)"
LanguageCodes( 334 ) = "ori" : LanguageNames( 334 ) = "Oriya"
LanguageCodes( 335 ) = "orm" : LanguageNames( 335 ) = "Oromo"
LanguageCodes( 336 ) = "osa" : LanguageNames( 336 ) = "Osage"
LanguageCodes( 337 ) = "oss" : LanguageNames( 337 ) = "Ossetic"
LanguageCodes( 338 ) = "oto" : LanguageNames( 338 ) = "Otomian languages"
LanguageCodes( 339 ) = "pal" : LanguageNames( 339 ) = "Pahlavi"
LanguageCodes( 340 ) = "pau" : LanguageNames( 340 ) = "Palauan"
LanguageCodes( 341 ) = "pli" : LanguageNames( 341 ) = "Pali"
LanguageCodes( 342 ) = "pam" : LanguageNames( 342 ) = "Pampanga"
LanguageCodes( 343 ) = "pag" : LanguageNames( 343 ) = "Pangasinan"
LanguageCodes( 344 ) = "pan" : LanguageNames( 344 ) = "Panjabi"
LanguageCodes( 345 ) = "pap" : LanguageNames( 345 ) = "Papiamento"
LanguageCodes( 346 ) = "paa" : LanguageNames( 346 ) = "Papuan (Other)"
LanguageCodes( 347 ) = "per" : LanguageNames( 347 ) = "Persian"
LanguageCodes( 348 ) = "phi" : LanguageNames( 348 ) = "Philippine (Other)"
LanguageCodes( 349 ) = "phn" : LanguageNames( 349 ) = "Phoenician"
LanguageCodes( 350 ) = "pon" : LanguageNames( 350 ) = "Pohnpeian"
LanguageCodes( 351 ) = "pol" : LanguageNames( 351 ) = "Polish"
LanguageCodes( 352 ) = "por" : LanguageNames( 352 ) = "Portuguese"
LanguageCodes( 353 ) = "pra" : LanguageNames( 353 ) = "Prakrit languages"
LanguageCodes( 354 ) = "pro" : LanguageNames( 354 ) = "Provençal (to 1500)"
LanguageCodes( 355 ) = "pus" : LanguageNames( 355 ) = "Pushto"
LanguageCodes( 356 ) = "que" : LanguageNames( 356 ) = "Quechua"
LanguageCodes( 357 ) = "roh" : LanguageNames( 357 ) = "Raeto-Romance"
LanguageCodes( 358 ) = "raj" : LanguageNames( 358 ) = "Rajasthani"
LanguageCodes( 359 ) = "rap" : LanguageNames( 359 ) = "Rapanui"
LanguageCodes( 360 ) = "rar" : LanguageNames( 360 ) = "Rarotongan"
LanguageCodes( 361 ) = "roa" : LanguageNames( 361 ) = "Romance (Other)"
LanguageCodes( 362 ) = "rom" : LanguageNames( 362 ) = "Romani"
LanguageCodes( 363 ) = "rum" : LanguageNames( 363 ) = "Romanian"
LanguageCodes( 364 ) = "run" : LanguageNames( 364 ) = "Rundi"
LanguageCodes( 365 ) = "rus" : LanguageNames( 365 ) = "Russian"
LanguageCodes( 366 ) = "sal" : LanguageNames( 366 ) = "Salishan languages"
LanguageCodes( 367 ) = "sam" : LanguageNames( 367 ) = "Samaritan Aramaic"
LanguageCodes( 368 ) = "smi" : LanguageNames( 368 ) = "Sami"
LanguageCodes( 369 ) = "smo" : LanguageNames( 369 ) = "Samoan"
LanguageCodes( 370 ) = "sad" : LanguageNames( 370 ) = "Sandawe"
LanguageCodes( 371 ) = "sag" : LanguageNames( 371 ) = "Sango (Ubangi Creole)"
LanguageCodes( 372 ) = "san" : LanguageNames( 372 ) = "Sanskrit"
LanguageCodes( 373 ) = "sat" : LanguageNames( 373 ) = "Santali"
LanguageCodes( 374 ) = "srd" : LanguageNames( 374 ) = "Sardinian"
LanguageCodes( 375 ) = "sas" : LanguageNames( 375 ) = "Sasak"
LanguageCodes( 376 ) = "sco" : LanguageNames( 376 ) = "Scots"
LanguageCodes( 377 ) = "gla" : LanguageNames( 377 ) = "Scottish Gaelic"
LanguageCodes( 378 ) = "sel" : LanguageNames( 378 ) = "Selkup"
LanguageCodes( 379 ) = "sem" : LanguageNames( 379 ) = "Semitic (Other)"
LanguageCodes( 380 ) = "srp" : LanguageNames( 380 ) = "Serbian"
LanguageCodes( 381 ) = "srr" : LanguageNames( 381 ) = "Serer"
LanguageCodes( 382 ) = "shn" : LanguageNames( 382 ) = "Shan"
LanguageCodes( 383 ) = "sna" : LanguageNames( 383 ) = "Shona"
LanguageCodes( 384 ) = "iii" : LanguageNames( 384 ) = "Sichuan Yi"
LanguageCodes( 385 ) = "scn" : LanguageNames( 385 ) = "Sicilian Italian"
LanguageCodes( 386 ) = "sid" : LanguageNames( 386 ) = "Sidamo"
LanguageCodes( 387 ) = "sgn" : LanguageNames( 387 ) = "Sign languages"
LanguageCodes( 388 ) = "bla" : LanguageNames( 388 ) = "Siksika"
LanguageCodes( 389 ) = "snd" : LanguageNames( 389 ) = "Sindhi"
LanguageCodes( 390 ) = "sin" : LanguageNames( 390 ) = "Sinhalese"
LanguageCodes( 391 ) = "sit" : LanguageNames( 391 ) = "Sino-Tibetan (Other)"
LanguageCodes( 392 ) = "sio" : LanguageNames( 392 ) = "Siouan (Other)"
LanguageCodes( 393 ) = "sms" : LanguageNames( 393 ) = "Skolt Sami"
LanguageCodes( 394 ) = "den" : LanguageNames( 394 ) = "Slavey"
LanguageCodes( 395 ) = "sla" : LanguageNames( 395 ) = "Slavic (Other)"
LanguageCodes( 396 ) = "slo" : LanguageNames( 396 ) = "Slovak"
LanguageCodes( 397 ) = "slv" : LanguageNames( 397 ) = "Slovenian"
LanguageCodes( 398 ) = "sog" : LanguageNames( 398 ) = "Sogdian"
LanguageCodes( 399 ) = "som" : LanguageNames( 399 ) = "Somali"
LanguageCodes( 400 ) = "son" : LanguageNames( 400 ) = "Songhai"
LanguageCodes( 401 ) = "snk" : LanguageNames( 401 ) = "Soninke"
LanguageCodes( 402 ) = "wen" : LanguageNames( 402 ) = "Sorbian (Other)"
LanguageCodes( 403 ) = "sot" : LanguageNames( 403 ) = "Sotho"
LanguageCodes( 404 ) = "sai" : LanguageNames( 404 ) = "South American Indian (Other)"
LanguageCodes( 405 ) = "sma" : LanguageNames( 405 ) = "Southern Sami"
LanguageCodes( 406 ) = "spa" : LanguageNames( 406 ) = "Spanish"
LanguageCodes( 407 ) = "srn" : LanguageNames( 407 ) = "Sranan"
LanguageCodes( 408 ) = "suk" : LanguageNames( 408 ) = "Sukuma"
LanguageCodes( 409 ) = "sux" : LanguageNames( 409 ) = "Sumerian"
LanguageCodes( 410 ) = "sun" : LanguageNames( 410 ) = "Sundanese"
LanguageCodes( 411 ) = "sus" : LanguageNames( 411 ) = "Susu"
LanguageCodes( 412 ) = "swa" : LanguageNames( 412 ) = "Swahili"
LanguageCodes( 413 ) = "ssw" : LanguageNames( 413 ) = "Swazi"
LanguageCodes( 414 ) = "swe" : LanguageNames( 414 ) = "Swedish"
LanguageCodes( 415 ) = "gsw" : LanguageNames( 415 ) = "Swiss German"
LanguageCodes( 416 ) = "syc" : LanguageNames( 416 ) = "Syriac"
LanguageCodes( 417 ) = "syr" : LanguageNames( 417 ) = "Syriac, Modern"
LanguageCodes( 418 ) = "tgl" : LanguageNames( 418 ) = "Tagalog"
LanguageCodes( 419 ) = "tah" : LanguageNames( 419 ) = "Tahitian"
LanguageCodes( 420 ) = "tai" : LanguageNames( 420 ) = "Tai (Other)"
LanguageCodes( 421 ) = "tgk" : LanguageNames( 421 ) = "Tajik"
LanguageCodes( 422 ) = "tmh" : LanguageNames( 422 ) = "Tamashek"
LanguageCodes( 423 ) = "tam" : LanguageNames( 423 ) = "Tamil"
LanguageCodes( 424 ) = "tat" : LanguageNames( 424 ) = "Tatar"
LanguageCodes( 425 ) = "tel" : LanguageNames( 425 ) = "Telugu"
LanguageCodes( 426 ) = "tem" : LanguageNames( 426 ) = "Temne"
LanguageCodes( 427 ) = "ter" : LanguageNames( 427 ) = "Terena"
LanguageCodes( 428 ) = "tet" : LanguageNames( 428 ) = "Tetum"
LanguageCodes( 429 ) = "tha" : LanguageNames( 429 ) = "Thai"
LanguageCodes( 430 ) = "tib" : LanguageNames( 430 ) = "Tibetan"
LanguageCodes( 431 ) = "tir" : LanguageNames( 431 ) = "Tigrinya"
LanguageCodes( 432 ) = "tig" : LanguageNames( 432 ) = "Tigré"
LanguageCodes( 433 ) = "tiv" : LanguageNames( 433 ) = "Tiv"
LanguageCodes( 434 ) = "tli" : LanguageNames( 434 ) = "Tlingit"
LanguageCodes( 435 ) = "tpi" : LanguageNames( 435 ) = "Tok Pisin"
LanguageCodes( 436 ) = "tkl" : LanguageNames( 436 ) = "Tokelauan"
LanguageCodes( 437 ) = "tog" : LanguageNames( 437 ) = "Tonga (Nyasa)"
LanguageCodes( 438 ) = "ton" : LanguageNames( 438 ) = "Tongan"
LanguageCodes( 439 ) = "tsi" : LanguageNames( 439 ) = "Tsimshian"
LanguageCodes( 440 ) = "tso" : LanguageNames( 440 ) = "Tsonga"
LanguageCodes( 441 ) = "tsn" : LanguageNames( 441 ) = "Tswana"
LanguageCodes( 442 ) = "tum" : LanguageNames( 442 ) = "Tumbuka"
LanguageCodes( 443 ) = "tup" : LanguageNames( 443 ) = "Tupi languages"
LanguageCodes( 444 ) = "tur" : LanguageNames( 444 ) = "Turkish"
LanguageCodes( 445 ) = "ota" : LanguageNames( 445 ) = "Turkish, Ottoman"
LanguageCodes( 446 ) = "tuk" : LanguageNames( 446 ) = "Turkmen"
LanguageCodes( 447 ) = "tvl" : LanguageNames( 447 ) = "Tuvaluan"
LanguageCodes( 448 ) = "tyv" : LanguageNames( 448 ) = "Tuvinian"
LanguageCodes( 449 ) = "twi" : LanguageNames( 449 ) = "Twi"
LanguageCodes( 450 ) = "udm" : LanguageNames( 450 ) = "Udmurt"
LanguageCodes( 451 ) = "uga" : LanguageNames( 451 ) = "Ugaritic"
LanguageCodes( 452 ) = "uig" : LanguageNames( 452 ) = "Uighur"
LanguageCodes( 453 ) = "ukr" : LanguageNames( 453 ) = "Ukrainian"
LanguageCodes( 454 ) = "umb" : LanguageNames( 454 ) = "Umbundu"
LanguageCodes( 455 ) = "und" : LanguageNames( 455 ) = "Undetermined"
LanguageCodes( 456 ) = "hsb" : LanguageNames( 456 ) = "Upper Sorbian"
LanguageCodes( 457 ) = "urd" : LanguageNames( 457 ) = "Urdu"
LanguageCodes( 458 ) = "uzb" : LanguageNames( 458 ) = "Uzbek"
LanguageCodes( 459 ) = "vai" : LanguageNames( 459 ) = "Vai"
LanguageCodes( 460 ) = "ven" : LanguageNames( 460 ) = "Venda"
LanguageCodes( 461 ) = "vie" : LanguageNames( 461 ) = "Vietnamese"
LanguageCodes( 462 ) = "vol" : LanguageNames( 462 ) = "Volapük"
LanguageCodes( 463 ) = "vot" : LanguageNames( 463 ) = "Votic"
LanguageCodes( 464 ) = "wak" : LanguageNames( 464 ) = "Wakashan languages"
LanguageCodes( 465 ) = "wln" : LanguageNames( 465 ) = "Walloon"
LanguageCodes( 466 ) = "war" : LanguageNames( 466 ) = "Waray"
LanguageCodes( 467 ) = "was" : LanguageNames( 467 ) = "Washoe"
LanguageCodes( 468 ) = "wel" : LanguageNames( 468 ) = "Welsh"
LanguageCodes( 469 ) = "him" : LanguageNames( 469 ) = "Western Pahari languages"
LanguageCodes( 470 ) = "wal" : LanguageNames( 470 ) = "Wolayta"
LanguageCodes( 471 ) = "wol" : LanguageNames( 471 ) = "Wolof"
LanguageCodes( 472 ) = "xho" : LanguageNames( 472 ) = "Xhosa"
LanguageCodes( 473 ) = "sah" : LanguageNames( 473 ) = "Yakut"
LanguageCodes( 474 ) = "yao" : LanguageNames( 474 ) = "Yao (Africa)"
LanguageCodes( 475 ) = "yap" : LanguageNames( 475 ) = "Yapese"
LanguageCodes( 476 ) = "yid" : LanguageNames( 476 ) = "Yiddish"
LanguageCodes( 477 ) = "yor" : LanguageNames( 477 ) = "Yoruba"
LanguageCodes( 478 ) = "ypk" : LanguageNames( 478 ) = "Yupik languages"
LanguageCodes( 479 ) = "znd" : LanguageNames( 479 ) = "Zande languages"
LanguageCodes( 480 ) = "zap" : LanguageNames( 480 ) = "Zapotec"
LanguageCodes( 481 ) = "zza" : LanguageNames( 481 ) = "Zaza"
LanguageCodes( 482 ) = "zen" : LanguageNames( 482 ) = "Zenaga"
LanguageCodes( 483 ) = "zha" : LanguageNames( 483 ) = "Zhuang"
LanguageCodes( 484 ) = "zul" : LanguageNames( 484 ) = "Zulu"
LanguageCodes( 485 ) = "zun" : LanguageNames( 485 ) = "Zuni"

End Sub

'****************************************************************************************

Function FieldOutput( DelimSymbol$ ) As String

Dim SubfieldCode$
Dim SubfieldCodeOriginal$
Dim TempField$
Dim TempString$

Dim i As Integer, j As Integer

For i = 1 To Len( SubfieldsUsed$ )
  SubfieldCode$ = Mid$( SubfieldsUsed$, i, 1 )

  If SubfieldCode$ Like "[abdefgijpqrt]" Then
      TempString$ = Subfields( ToggleCodeIndex( SubfieldCode$ ) ).tPrimaryLangs
      If TempString$ <> "" Then
          TempString$ = FormatSubfieldsForOutput( TempString$, SubfieldCode$, DelimSymbol$ )
      End If

      TempField$ = TempField$ & TempString$

      TempString$ = Subfields( ToggleCodeIndex( SubfieldCode$ ) ).tInterLang
      If TempString$ <> "" Then
          TempString = FormatSubfieldsForOutput( TempString$, "k", DelimSymbol$ )
      End If

      TempField$ = TempField$ & TempString$

      TempString$ = Subfields( ToggleCodeIndex( SubfieldCode$ ) ).tOriginalLang
      If TempString$ <> "" Then
          SubfieldCodeOriginal$ = Subfields( ToggleCodeIndex( SubfieldCode$ ) ).tOriginalLangCode

' The subfield code for the original language of a resource depends on what the primary
' language is. For most material the subfield code is $h, but for original accompanying
' materials other than librettos, the code is $m, and for the original libretto, it is
' $n. For other subfields, it is simply not possible to add language codes for an
' original language, such as for tables of contents (subfield $f).

          If SubfieldCodeOriginal$ = "" Then
              If SubfieldCode$ Like "[adhk]" Then
                  SubfieldCodeOriginal$ = "h"
                ElseIf SubfieldCode$ Like "[bg]" Then
                  SubfieldCodeOriginal$ = "m"
                ElseIf SubfieldCode$ = "e" Then
                  SubfieldCodeOriginal$ = "n"
              End If
          End If
          TempString = FormatSubfieldsForOutput( TempString$, SubfieldCodeOriginal$, DelimSymbol$ )
      End If

      TempField$ = TempField$ & TempString$
  End If
Next i

FieldOutput = Trim$( TempField$ )

End Function

'****************************************************************************************

Function FindCodesForNames( InputString$, High%, Low% ) As Integer

' This function searches the list of names to find the appropriate code when a name has
' been selected by the dialog box search. It can use a linear binary search because the
' list of names is already in alphabetical order, unlike the list of codes.

Dim Beyond$
Dim Compare%
Dim Pivot%
Dim Result%
Dim Test$

Dim Match : Match = FALSE

Dim i As Integer

Do
  Pivot% = Int( ( High% + Low% ) / 2 )
'  Test$    = LanguageNames( Pivot% )
  Compare% = StrComp( LanguageNames( Pivot% ), InputString$ )
  Select Case Compare%
    Case -1
      Low%    = Pivot%
    Case 0
      Match   = TRUE
      Result% = Pivot%
      Exit Do
    Case 1
      High%   = Pivot%
  End Select
Loop Until Match = TRUE Or Beyond$ <> ""

If Beyond$ <> "" Then Result% = 0

FindCodesForNames = Result%

End Function

'****************************************************************************************

Function FormatSubfieldsForOutput( InString$, SubfCode$, DelimiterSymbol$ )

' This function takes a string of language codes and formats it for use in the field,
' adding the supplied delimiter symbol (the dollar sign for display in the dialog box,
' the traditional OCLC alveolar click for the bibliographic record) and subfield code
' before each language code.

Dim LangCode$
Dim TempField$
Dim TempSubfield$

Dim i As Integer

i = 1
Do
  LangCode$ = GetField$( InString$, i, " " )
  If LangCode$ <> "" Then
      LangCode$     = DelimiterSymbol$ & SubfCode$ & " " & GetField$( InString$, i, " " ) & " "
      TempSubfield$ = TempSubfield$ & LangCode$
      i = i + 1
  End If
Loop Until LangCode$ = ""
TempField$    = TempField$ & TempSubfield$
TempSubfield$ = ""

FormatSubfieldsForOutput = TempField$

End Function

'****************************************************************************************

Function SortSubfieldCodes( InString$ ) As String

' This function uses an inefficient but simple to code bubble sort to arrange the codes
' in alphabetical order in subfields $b and $f.

Dim CodeCount%
Dim Code1$
Dim Code2$
Dim Start%
Dim TempField$

Dim Swapped

Dim i As Integer, p As Integer

TempField$ = Trim$( InString$ )

' First, the number of elements to be sorted must be found; this is done by counting the
' spaces in the string, which serve as separators in OML's "GetField" function. The
' number of spaces will be one less than the number of elements, which conveniently is
' how the upper limit of this routine is established when run on an array.

Start% = 1
Do
  p = InStr( Start%, TempField$, " " )
  If p > 0 Then
      CodeCount% = CodeCount% + 1
      Start% = p + 1
    Else
      If Start% = 1 Then
          SortSubfieldCodes = TempField$
          Exit Function
      End If
  End If
Loop Until p = 0

' Then do the sort, which simply repeats swapping adjacent elements when they are out of
' order until no more swapping has been done.

Do
  Swapped = FALSE
  For i = 1 To CodeCount%
    Code1$ = GetField$( TempField$, i, " " )
    Code2$ = GetField$( TempField$, i + 1, " " )
    If Code1$ > Code2$ Then
        TempField$ = SetField$( TempField$, i, Code2$, " " )
        TempField$ = SetField$( TempField$, i + 1, Code1$, " " )
        Swapped    = TRUE
    End If
  Next i
Loop Until Swapped = FALSE

SortSubfieldCodes = Trim$( TempField$ )

End Function

'****************************************************************************************

Function ToggleCodeIndex( InVal As Variant ) As Variant

' This function converts a subfield code to the index of the array and vice versa.

Const VAR_TYPE_INTEGER As Integer = 2

If VarType( InVal ) = VAR_TYPE_INTEGER Then
    Select Case InVal
      Case aA
        ToggleCodeIndex = "a"
      Case aB
        ToggleCodeIndex = "b"
      Case aD
        ToggleCodeIndex = "d"
      Case aE
        ToggleCodeIndex = "e"
      Case aF
        ToggleCodeIndex = "f"
      Case aG
        ToggleCodeIndex = "g"
      Case aI
        ToggleCodeIndex = "i"
      Case aJ
        ToggleCodeIndex = "j"
      Case aP
        ToggleCodeIndex = "p"
      Case aQ
        ToggleCodeIndex = "q"
      Case aR
        ToggleCodeIndex = "r"
      Case aT
        ToggleCodeIndex = "t"
    End Select
  Else
    Select Case InVal
      Case "a"
        ToggleCodeIndex = aA
      Case "b"
        ToggleCodeIndex = aB
      Case "d"
        ToggleCodeIndex = aD
      Case "e"
        ToggleCodeIndex = aE
      Case "f"
        ToggleCodeIndex = aF
      Case "g"
        ToggleCodeIndex = aG
      Case "i"
        ToggleCodeIndex = aI
      Case "j"
        ToggleCodeIndex = aJ
      Case "p"
        ToggleCodeIndex = aP
      Case "q"
        ToggleCodeIndex = aQ
      Case "r"
        ToggleCodeIndex = aR
      Case "t"
        ToggleCodeIndex = aT
    End Select
End If

End Function

'****************************************************************************************

Function Dialog1ControlFunction( Id$, Action%, SVal& )

Const CONTROL_CHANGE As Integer = 2
Const DISABLED       As Integer = 0
Const ENABLED        As Integer = 1
Const FOCUS_CHANGE   As Integer = 4
Const IDLE_STATE     As Integer = 5
Const INITIALIZE     As Integer = 1
Const INVISIBLE      As Integer = 0
Const KEEP_DLG_OPEN  As Integer = -1
Const VISIBLE        As Integer = 1

Const ADD     As String = "&Add field"
Const KLOSE   As String = "&Close"
Const REPLACE As String = "&Replace field"

Static CurrDisplayField$
Static CurrSubfInter$
Static CurrSubfOriginal$
Static CurrSubfieldCode$
Static InitialSubfSelection%
Static MismatchLegend$
Static PrevDisplayField$
Static PrevValTBIntermediate$
Static PrevValTBOriginal$
Static PrevValTBPrimaries$

Static IgnoreMismatch
Static NewSubfield
Static SearchDone

Dim ArrayIndex%
Dim CurrValTBIntermediate$
Dim CurrValTBOriginal$
Dim CurrValTBPrimaries$
Dim LangSearchResultsSelection%
Dim LanguageSelection%
Dim SearchStringForCodes$
Dim SearchedCode$
Dim SelectedLanguage$
Dim SubfieldCodeSelection%
Dim SubfieldCodeTest$
Dim SubfieldListSelection%
Dim TempString$

Dim i As Integer, p As Integer

MismatchLegend$ = "The language code in the fixed field does not match the first language code in the 041 field. Select an option to continue:"

Select Case Action%

  Case INITIALIZE

ReShow:

    NewSubfield = TRUE

    If AddNew Then
        DlgText         "DialogBoxTopText",     "NEW 041 FIELD BEING CREATED! Click on a subfield to add language codes:"
        DlgText         "OK",                   ADD
        DlgFocus        "PrimariesTextBox"
      Else
        DlgText         "DialogBoxTopText",     "Select a subfield to view or edit its language codes:"
    End If

' The default selection in the list of subfield codes is either dependent on the type of
' record (music or not) or on the first subfield actually used.

    If Left$( SubfieldsUsed$, 1 ) = ToggleCodeIndex( TypeOfRecord% ) Then
        InitialSubfSelection% = TypeOfRecord%
      Else
        InitialSubfSelection% = ToggleCodeIndex( Left$( SubfieldsUsed$, 1 ) )
    End If

' Fill the text boxes with values from the array, if any, and make the previous values in
' those text boxes the same, so the macro won't think there's anything to replace at this
' point.

    DlgValue        "SubfieldsListBox",     InitialSubfSelection%
    DlgText         "PrimariesTextBox",     Subfields( InitialSubfSelection% ).tPrimaryLangs
    DlgText         "IntermediatesTextBox", Subfields( InitialSubfSelection% ).tInterLang
    DlgText         "OriginalsTextBox",     Subfields( InitialSubfSelection% ).tOriginalLang
    PrevValTBPrimaries$    = DlgText( "PrimariesTextBox" )
    PrevValTBIntermediate$ = DlgText( "IntermediatesTextBox" )
    PrevValTBOriginal$     = DlgText( "OriginalsTextBox" )
    CurrSubfieldCode$      = ToggleCodeIndex( InitialSubfSelection% )
    CurrSubfOriginal$      = Subfields( InitialSubfSelection% ).tOriginalLangCode
    If CurrSubfOriginal$ = "" Then
        Select Case CurrSubfieldCode$
          Case "a", "d"
            CurrSubfOriginal$ = "h"
          Case "b", "g"
            CurrSubfOriginal$ = "m"
          Case "e"
            CurrSubfOriginal$ = "n"
        End Select
      Else
    End If
    CurrSubfInter$ = "k"
    DlgText         "OriginalLang",         "Original language(s) [subfield $" & CurrSubfOriginal$ & "]"
    DlgText         "DisplayField",         CurrDisplayField$
    DlgText         "ViewCodesLegend",      "Language names in subfield $" & CurrSubfieldCode$ & ":"
    RelatedSubfields( 0 ) = CurrSubfieldCode$
    RelatedSubfields( 1 ) = CurrSubfInter$
    RelatedSubfields( 2 ) = CurrSubfOriginal$
    DlgListBoxArray "SubfieldTrioListBox",  RelatedSubfields()
    DlgValue        "SubfieldTrioListBox",  0
    DlgVisible      "AddButton",            INVISIBLE
    DlgVisible      "AddCodePrompt",        INVISIBLE
    DlgVisible      "SubfieldTrioListBox",  INVISIBLE
    DlgText         "ViewCodesGroupBox",    "View language codes"
    MatchNamesToCodes( DlgText( "PrimariesTextBox" ) )
    DlgListBoxArray "ViewCodesListBox",     ShowCodesNames()
    If IgnoreMismatch <> TRUE Then
        IgnoreMismatch = FALSE
    End If
    DlgVisible      "FFChangeNote",         INVISIBLE


  Case CONTROL_CHANGE

    Select Case Id$

      Case "SubfieldsListBox"

        SubfieldListSelection% = DlgValue( "SubfieldsListBox" )
        CurrSubfieldCode$      = ToggleCodeIndex( SubfieldListSelection% )

' Some subfields don't allow for a language of original, so disable the controls for
' those subfields.

        If CurrSubfieldCode$ Like "[abdeghk]" Then
            DlgEnable       "OriginalLang",         ENABLED
            DlgEnable       "OriginalsTextBox",     ENABLED
          Else
            DlgEnable       "OriginalLang",         DISABLED
            DlgEnable       "OriginalsTextBox",     DISABLED
        End If

' Go through the list of subfields to see if this is a new subfield being added, or an
' existing subfield being added to.

        For i = 1 To Len( SubfieldsUsed$ )
          SubfieldCodeTest$ = Mid$( SubfieldsUsed$, i, 1 )
          If SubfieldCodeTest$ Like "[abdefgijpqrt]" Then
              ArrayIndex% = ToggleCodeIndex( SubfieldCodeTest$ )
          End If
          If SubfieldCodeTest$ = CurrSubfieldCode$ Then   'ADDING TO AN EXISTING SUBFIELD!
              NewSubfield            = FALSE
              PrevValTBPrimaries$    = Subfields( ArrayIndex% ).tPrimaryLangs  'Make the existing codes the previous values
              PrevValTBIntermediate$ = Subfields( ArrayIndex% ).tInterLang
              PrevValTBOriginal$     = Subfields( ArrayIndex% ).tOriginalLang
              CurrSubfOriginal$      = Subfields( ArrayIndex% ).tOriginalLangCode
              DlgText         "PrimariesTextBox",     Subfields( SubfieldListSelection% ).tPrimaryLangs
              DlgText         "IntermediatesTextBox", Subfields( SubfieldListSelection% ).tInterLang
              DlgText         "OriginalsTextBox",     Subfields( SubfieldListSelection% ).tOriginalLang
              MatchNamesToCodes( DlgText( "PrimariesTextBox" ) )
              DlgListBoxArray "ViewCodesListBox",     ShowCodesNames()
              Exit For
          End If
        Next i

        If NewSubfield Then
            SubfieldsUsed$ = SubfieldsUsed$ & CurrSubfieldCode$
            DlgText         "PrimariesTextBox",     ""
            DlgText         "IntermediatesTextBox", ""
            DlgText         "OriginalsTextBox",     ""
            PrevValTBPrimaries$    = ""
            PrevValTBIntermediate$ = ""
            PrevValTBOriginal$     = ""
            Select Case CurrSubfieldCode$
              Case "a", "d", "f", "j"
                CurrSubfOriginal$ = "h"
              Case "b", "g"
                CurrSubfOriginal$ = "m"
              Case "e"
                CurrSubfOriginal$ = "n"
            End Select
            DlgText         "OriginalLang",         "Original language(s) [subfield $" & CurrSubfOriginal$ & "]"
        End If
        DlgFocus        "PrimariesTextBox"

        RelatedSubfields( 0 ) = CurrSubfieldCode$
        RelatedSubfields( 1 ) = CurrSubfInter$
        RelatedSubfields( 2 ) = CurrSubfOriginal$
        DlgListBoxArray "SubfieldTrioListBox",      RelatedSubfields()

      Case "LanguageSelectionListBox"

        LanguageSelection% = DlgValue( "LanguageSelectionListBox" )
        ReDim ShowCodesNames( 0 )
        ShowCodesNames( 0 ) = LanguageNames( LanguageSelection% )
        DlgListBoxArray "ViewCodesListBox",         ShowCodesNames()
        DlgValue        "ViewCodesListBox",         0
        DlgVisible      "AddButton",                VISIBLE
        DlgVisible      "AddCodePrompt",            VISIBLE
        DlgVisible      "SubfieldTrioListBox",      VISIBLE
        DlgFocus        "AddButton"
        DlgText         "ViewCodesGroupBox",        "Add language codes"
        DlgText         "ViewCodesLegend",          "Add code for this language ..."
        SearchDone = TRUE


      Case "SearchButton"

        SearchStringForCodes$ = DlgText( "SearchBox" )
        Call SearchLanguageNames( SearchStringForCodes$ )
        DlgListBoxArray "ViewCodesListBox",     ShowCodesNames()
        SearchDone            = TRUE
        DlgText         "SearchBox",            ""
        If Left$( ShowCodesNames( 0 ), 8 ) = "No match" Then
            DlgText         "ViewCodesLegend",      "Select a language to add to a subfield:"
            DlgVisible      "AddButton",            INVISIBLE
            DlgVisible      "AddCodePrompt",        INVISIBLE
            DlgVisible      "SubfieldTrioListBox",  INVISIBLE
            DlgFocus        "OK"
          Else
            If UBound( ShowCodesNames ) = 0 Then
                DlgValue        "ViewCodesListBox",     0
                DlgText         "ViewCodesLegend",      "Add the code for this language ..."
                DlgVisible      "AddButton",            VISIBLE
                DlgVisible      "AddCodePrompt",        VISIBLE
                DlgVisible      "SubfieldTrioListBox",  VISIBLE
              Else
                DlgText         "ViewCodesLegend",      "Select a language to add its code ..."
                DlgVisible      "AddButton",            INVISIBLE
                DlgVisible      "AddCodePrompt",        INVISIBLE
                DlgVisible      "SubfieldTrioListBox",  INVISIBLE
            End If
        End If
        Dialog1ControlFunction = KEEP_DLG_OPEN

      Case "AddButton"

        LangSearchResultsSelection% = DlgValue( "ViewCodesListBox" )
        SearchStringForCodes$       = DlgText( "ViewCodesListBox" )
        If InStr( SearchStringForCodes$, "=" ) Then
            SearchedCode$ = Left$( SearchStringForCodes$, 3 )
          Else
            SearchedCode$ = LanguageCodes( FindCodesForNames( SearchStringForCodes$, LANGUAGE_CODES_COUNT, 0 ) )
        End If
        SubfieldCodeSelection%      = DlgValue( "SubfieldTrioListBox" )
        ArrayIndex%                 = ToggleCodeIndex( CurrSubfieldCode$ )
        Select Case SubfieldCodeSelection%
          Case 0
            Subfields( ArrayIndex% ).tPrimaryLangs = Subfields( ArrayIndex% ).tPrimaryLangs & SearchedCode$ & " "
            DlgText         "PrimariesTextBox",     Subfields( ArrayIndex% ).tPrimaryLangs
            DlgFocus        "PrimariesTextBox"
          Case 1
            Subfields( ArrayIndex% ).tInterLang    = Subfields( ArrayIndex% ).tInterLang & SearchedCode$ & " "
            DlgText         "IntermediatesTextBox", Subfields( ArrayIndex% ).tInterLang
            DlgFocus        "IntermediatesTextBox"
          Case 2
            Subfields( ArrayIndex% ).tOriginalLang = Subfields( ArrayIndex% ).tOriginalLang & SearchedCode$ & " "
            DlgText         "OriginalsTextBox",     Subfields( ArrayIndex% ).tOriginalLang
            DlgFocus        "OriginalsTextBox"
        End Select
        DlgVisible      "AddButton",            INVISIBLE
        DlgVisible      "AddCodePrompt",        INVISIBLE
        DlgVisible      "SubfieldTrioListBox",  INVISIBLE
        Dialog1ControlFunction = KEEP_DLG_OPEN

      Case "ViewCodesListBox"

        If SearchDone Then
            LangSearchResultsSelection% = DlgValue( "ViewCodesListBox" )
            SearchStringForCodes$       = Trim$( DlgText( "ViewCodesListBox" ) )
            If Left$( SearchStringForCodes$, 8 ) <> "No match" Then
                If LangSearchResultsSelection% = 0 And Len( SearchStringForCodes$ ) = 3 Then
                    SearchedCode$ = SearchStringForCodes$
                  Else
                    SearchedCode$ = LanguageCodes( FindCodesForNames( SearchStringForCodes$, LANGUAGE_CODES_COUNT, 0 ) )
                End If
                ArrayIndex% = ToggleCodeIndex( CurrSubfieldCode$ )
                DlgVisible      "AddButton",            VISIBLE
                DlgVisible      "AddCodePrompt",        VISIBLE
                DlgVisible      "SubfieldTrioListBox",  VISIBLE
                DlgText         "SearchBox",               ""
            End If
            SearchDone = FALSE
        End If

    End Select


  Case FOCUS_CHANGE

    Select Case Id$

      Case "PrimariesTextBox"
        MatchNamesToCodes( DlgText( "PrimariesTextBox" ) )
        DlgListBoxArray "ViewCodesListBox",     ShowCodesNames()
        DlgText         "ViewCodesLegend",      "Language names in subfield $" & CurrSubfieldCode$ & ":"
        DlgText         "SearchBox",            ""

      Case "IntermediatesTextBox"
        MatchNamesToCodes( DlgText( "IntermediatesTextBox" ) )
        DlgListBoxArray "ViewCodesListBox",     ShowCodesNames()
        DlgText         "ViewCodesLegend",      "Language names in subfield $k:"
        DlgText         "SearchBox",            ""

      Case "OriginalsTextBox"
        MatchNamesToCodes( DlgText( "OriginalsTextBox" ) )
        DlgListBoxArray "ViewCodesListBox",     ShowCodesNames()
        DlgText         "ViewCodesLegend",      "Language names in subfield $" & CurrSubfOriginal$ & ":"
        DlgText         "SearchBox",            ""

    End Select


  Case IDLE_STATE

' The dialog box continuously monitors the input boxes, and adjusts output depending on
' the changes made to the contents of those boxes. The first check it makes is if the
' language code in the fixed field does not match the first language code in the 041
' field. If the two codes are different, the macro presents three options: to immediately
' change the fixed field to match the 041, to change the first code in the 041 field to
' match the fixed field, or to ignore the mismatch and continue.

    If AddNew = FALSE Then
        If ( FirstCode$ <> FF_Lang$ ) Then
            If IgnoreMismatch = FALSE Then

                Begin Dialog Dialog2Definition  160, 104, WaltsMacros$
                  OkButton        38,  80,  52,  14
                  CancelButton    98,  80,  52,  14
                  Text            10,   6, 140,  24, MismatchLegend$
                  OptionGroup                                                   .OptionGroup1
                   OptionButton   18,  34,  88,  12, "Change &fixed field now", .OptionButton1
                   OptionButton   18,  46,  88,  12, "Change &041",             .OptionButton2
                   OptionButton   18,  58,  88,  12, "&Ignore difference",      .OptionButton3
                End Dialog

                Dim Dialog2 As Dialog2Definition
                If Dialog( Dialog2 ) = 0 Then
                    DlgEnd 0
                  Else
                    Select Case Dialog2.OptionGroup1
                      Case 0
                        If CS.SetFixedField( "Lang", FirstCode$ ) = FALSE Then
                            MsgBox "Sorry, could not update the language code in the fixed field.", WARNING_MESSAGE, WaltsMacros$
                          Else
                            IgnoreMismatch = TRUE
                            DlgVisible      "FFChangeNote",         VISIBLE
                        End If
                      Case 1
                        Mid$( Subfields( InitialSubfSelection% ).tPrimaryLangs, 1, 3 ) = FF_Lang$
                        IgnoreMismatch = TRUE
                        GoTo ReShow:
                      Case 2
                        IgnoreMismatch = TRUE
                    End Select
                End If
            End If
        End If
    End If

    CurrValTBPrimaries$ = LCase$( Trim$( DlgText( "PrimariesTextBox" ) ) )
    If CurrValTBPrimaries$ <> Trim$( PrevValTBPrimaries$ ) Then
        ArrayIndex% = ToggleCodeIndex( CurrSubfieldCode$ )
        Subfields( ArrayIndex% ).tPrimaryLangs = CurrValTBPrimaries$ & " "
        PrevValTBPrimaries$ = CurrValTBPrimaries$
        If InStr( SubfieldsUsed$, CurrSubfieldCode$ ) = FALSE Then
            SubfieldsUsed$ = SubfieldsUsed$ & CurrSubfieldCode$
        End If
        MatchNamesToCodes( CurrValTBPrimaries$ )
        DlgListBoxArray "ViewCodesListBox",     ShowCodesNames()
    End If

    CurrValTBIntermediate$ = LCase$( Trim$( DlgText( "IntermediatesTextBox" ) ) )
    CurrSubfInter$         = "k"
    ArrayIndex%            = ToggleCodeIndex( CurrSubfieldCode$ )
    If CurrValTBIntermediate$ <> Trim$( PrevValTBIntermediate$ ) Then
        Subfields( ToggleCodeIndex( CurrSubfieldCode$ ) ).tInterLang = CurrValTBIntermediate$ & " "
        PrevValTBIntermediate$ = CurrValTBIntermediate$
        If InStr( SubfieldsUsed$, CurrSubfInter$ ) = FALSE Then
            SubfieldsUsed$ = SubfieldsUsed$ & CurrSubfInter$
        End If
        MatchNamesToCodes( CurrValTBIntermediate$ )
        DlgListBoxArray "ViewCodesListBox",     ShowCodesNames()
    End If

    CurrValTBOriginal$ = LCase$( Trim$( DlgText( "OriginalsTextBox" ) ) )
    If CurrValTBOriginal$ <> Trim$( PrevValTBOriginal$ ) Then

' Don't change the value of an existing translation subfield unless the current
' translation subfield code is appropriate for the current primary subfield.

        ArrayIndex% = ToggleCodeIndex( CurrSubfieldCode$ )
        If CurrSubfOriginal$ = "h" Then
            If CurrSubfieldCode$ Like "[ad]" Then
                Subfields( ToggleCodeIndex( CurrSubfieldCode$ ) ).tOriginalLang = CurrValTBOriginal$ & " "
            End If
          ElseIf CurrSubfOriginal$ = "m" Then
            If CurrSubfieldCode$ Like "[bg]" Then
                Subfields( ToggleCodeIndex( CurrSubfieldCode$ ) ).tOriginalLang = CurrValTBOriginal$ & " "
            End If
          ElseIf CurrSubfOriginal$ = "n" Then
            If CurrSubfieldCode$ = "e" Then
                Subfields( ToggleCodeIndex( CurrSubfieldCode$ ) ).tOriginalLang = CurrValTBOriginal$ & " "
            End If
        End If
        PrevValTBOriginal$ = CurrValTBOriginal$
        If InStr( SubfieldsUsed$, CurrSubfOriginal$ ) = FALSE Then
            SubfieldsUsed$ = SubfieldsUsed$ & CurrSubfOriginal$
        End If
        MatchNamesToCodes( CurrValTBOriginal$ )
        DlgListBoxArray "ViewCodesListBox",     ShowCodesNames()
    End If

' If a subfield contains at least one code, mark it with an arrow to show it's been used.
' If the subfield is empty or has been emptied, remove the arrow.

    If CurrValTBPrimaries$ = "" And CurrValTBIntermediate$ = "" And CurrValTBOriginal$ = "" Then
        If Left$( SubfieldList( ArrayIndex% ), 1 ) <> " " Then
            SubfieldList( ArrayIndex% ) = SPACE_PREFIX & Mid$( SubfieldList( ArrayIndex% ), 5 )
            DlgListBoxArray "SubfieldsListBox",     SubfieldList
            DlgValue        "SubfieldsListBox",     ArrayIndex%
        End If
      Else
        If Left$( SubfieldList( ArrayIndex% ), 1 ) <> "=" Then
            SubfieldList( ArrayIndex% ) = ARROW_PREFIX & Mid$( SubfieldList( ArrayIndex% ), 7 )
            DlgListBoxArray "SubfieldsListBox",     SubfieldList
            DlgValue        "SubfieldsListBox",     ArrayIndex%
        End If
    End If

' The "Search" button is disabled until the search string is at least three characters in
' length.

    If Len( Trim$( DlgText( "SearchBox" ) ) ) < 3 Then
        DlgEnable       "SearchButton",         DISABLED
      Else
        DlgEnable       "SearchButton",         ENABLED
        DlgEnable       "AddButton",            ENABLED
        DlgEnable       "AddCodePrompt",        ENABLED
        DlgEnable       "SubfieldTrioListBox",  ENABLED
    End If

' The presence of a subfield for an original or intermediate language changes the value
' of the first indicator to "1".

    For i = 1 To 3
      If InStr( SubfieldsUsed$, Mid$( "hkn", i, 1 ) ) Then
          Ind1$ = "1"
          Exit For
      End If
    Next i

' Construct the display field, and show it if it differs from its previous version.

    CurrDisplayField$ = "041 " & Ind1$ & " " & Ind2$ & " " & FieldOutput( "$" )
    If CurrDisplayField$ <> PrevDisplayField$ Then
        DlgText         "DisplayField",         CurrDisplayField$
    End If
    PrevDisplayField$ = CurrDisplayField$

' Construct the new field (which is actually the replacement field if there is an
' existing 041 field), and change the text on the OK button to suit.

    NewField$ = Trim$( FieldOutput( DELIMITER ) )
    If Left$( NewField$, 2 ) = DELIMITER & "a" Then
        NewField$ = trim$( Mid$ (NewField$, 3 ) )
    End If
    NewField$ = "041" & Ind1$ & Ind2$ & NewField$

' If the field contains only a single language code, it shouldn't be added, so the text
' on the action button is just "Close," as it is when the display field is the same as
' the original field Otherwise, set the text on the action button to make it clear that
' clicking it will add a new field or replace an existing one.

    If Len( NewField$ ) < 9 Then
        If DlgText( "OK" ) <> KLOSE Then
            DlgText         "OK",                   KLOSE
        End If
      Else
        If AddNew Then
            If DlgText( "OK" ) <> ADD Then
                DlgText         "OK",                   ADD
            End If
          Else
            If NewField$ <> OriginalField$ Then
                If DlgText( "OK" ) <> REPLACE Then
                    DlgText         "OK",                   REPLACE
                End If
              Else
                If DlgText( "OK" ) <> KLOSE Then
                    DlgText         "OK",                   KLOSE
                End If
            End If
        End If
    End If

    Dialog1ControlFunction = KEEP_DLG_OPEN

End Select

End Function
'201193689
'
'Macro name: Show041B
'Macro book: C:\Users\wnickeson.UR\AppData\Roaming\OCLC\Connex\Macros\Extras1.mbk
'Saved: 5/30/2023 10:57:30 AM using "MacroBookInspector" macro by Walter F. Nickeson.
