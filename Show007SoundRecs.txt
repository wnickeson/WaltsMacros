'MacroName:Show007SoundRecs.2024.01
'MacroDescription:Displays the codes in 007 fields in a bibliographic record for sound
' recordings in a friendly way for verification and/or correction.
'
' This macro was written by Walter F. Nickeson,
' University of Rochester, Rochester, NY
' wnickeson@library.rochester.edu
' https://orcid.org/0000-0003-0484-6938
'
' Thank to Joel Hahn for some good ideas for an earlier draft of this macro. However, all
' responsibility for this macro's appearance and functionality are mine alone.
'
' Last updated: 9 February 2024.
' Check for the latest versions of this and my other macros at
' https://github.com/wnickeson/WaltsMacros
' Please e-mail me with bug reports or to suggest improvements.
'
' This macro works for me with Connexion client 3.1 and Windows 11 Enterprise & 64-bit
' Windows 10 Pro, but no guarantees are promised or implied.
'
'***************************************************************************************#
' How it works: Run the macro in a bibliographic record. If no 007 field for a sound
' recording is found, the macro offers to add one, the default values being those for
' compact discs. (All default values can be set in the "SetDefaults" subroutine, starting
' on line 644.) If sound recordings 007 fields are found, the macro shows their
' codes with explanations in a dialog box with drop-down lists, so the values can easily
' be verified and changed if necessary. Additional 007 fields can be added with the
' default values defined at the end of the macro. The macro does no error-checking,
' leaving that up to you.
'****************************************************************************************

Option Explicit

Type FieldContents
  tCaptureAndStorage       As String*1
  tChanged                 As String*1
  tDimensions              As String*1
  tFieldOccurrence         As Integer
  tGroove                  As String*1
  tKindOfCutting           As String*1
  tKindOfMaterial          As String*1
  tKindOfObject            As String*1
  tOriginalField           As String*12
  tPlaybackChannels        As String*1
  tPlaybackCharacteristics As String*1
  tSMD                     As String*1
  tSpeed                   As String*1
  tTapeConfiguration       As String*1
  tTapeWidth               As String*1
End Type

Declare Sub FillArrays
Declare Sub SetDefaults  ( Which007% )
Declare Sub SetListValues( Which007% )

Declare Function ListSelection( Which007%, WhichField$ ) As Integer

Declare Function Dialog1ControlFunction( Id$, Action%, SVal& )

Global Const ADD           As String  = "A"  'The flag to add a new field.
Global Const ANSWER_YES    As Integer = 6    'The value returned by a message box when the "Yes" button is clicked.
Global Const BLANK         As String  = " "
Global Const REPLACE       As String  = "R"  'The flag to replace an existing field.
Global Const UNCHANGED     As String  = "U"  'The flag that the macro has not changed a field.
Global Const WARNING_QUERY As Integer = 32   'The value to display the "Warning query" icon in a message box.
Global Const YES_NO        As Integer = 4    'The value to display "Yes" and "No" buttons in a message box.

Global NewField%
Global SR007Count%
Global SR007Instance%
Global WaltsMacros$

Global Changed
Global Missing

Global FieldStorage()         As FieldContents
Global Subfield_B_Array( 11 ) As String
Global Subfield_D_Array( 16 ) As String
Global Subfield_E_Array( 5  ) As String
Global Subfield_F_Array( 5  ) As String
Global Subfield_G_Array( 13 ) As String
Global Subfield_H_Array( 7  ) As String
Global Subfield_I_Array( 9  ) As String
Global Subfield_J_Array( 11 ) As String
Global Subfield_K_Array( 13 ) As String
Global Subfield_L_Array( 4  ) As String
Global Subfield_M_Array( 11 ) As String
Global Subfield_N_Array( 6  ) As String

'****************************************************************************************

Sub Main

Dim CS As Object
On Error Resume Next
Set CS = GetObject( , "Connex.Client" )
On Error GoTo 0
If CS Is Nothing Then Set CS = CreateObject( "Connex.Client" )

Const CRITICAL_MESSAGE     As Integer = 16
Const DIALOG_BUTTON_CANCEL As Integer = 0
Const INFORMATION_MESSAGE  As Integer = 64

Dim DELIMITER  As String*1   : DELIMITER      = Chr$( 223 )
Dim FILLCHAR   As String*1   : FILLCHAR       = Chr$( 252 )
Dim FILLCHAR_X As String*1   : FILLCHAR_X     = Chr$( 127 )
Dim SUBFIELD_B As String*2   : SUBFIELD_B     = DELIMITER & "b"
Dim SUBFIELD_D As String*2   : SUBFIELD_D     = DELIMITER & "d"
Dim SUBFIELD_E As String*2   : SUBFIELD_E     = DELIMITER & "e"
Dim SUBFIELD_F As String*2   : SUBFIELD_F     = DELIMITER & "f"
Dim SUBFIELD_G As String*2   : SUBFIELD_G     = DELIMITER & "g"
Dim SUBFIELD_H As String*2   : SUBFIELD_H     = DELIMITER & "h"
Dim SUBFIELD_I As String*2   : SUBFIELD_I     = DELIMITER & "i"
Dim SUBFIELD_J As String*2   : SUBFIELD_J     = DELIMITER & "j"
Dim SUBFIELD_K As String*2   : SUBFIELD_K     = DELIMITER & "k"
Dim SUBFIELD_L As String*2   : SUBFIELD_L     = DELIMITER & "l"
Dim SUBFIELD_M As String*2   : SUBFIELD_M     = DELIMITER & "m"
Dim SUBFIELD_N As String*2   : SUBFIELD_N     = DELIMITER & "n"


Dim Added%
Dim ConcludingMessage$
Dim Count007%
Dim Field007String$
Dim FieldData$
Dim FieldPosition%
Dim Instance%
Dim MandatoryText$           : MandatoryText$ = "* Please supply values for mandatory subfields!"
Dim Replaced%
Dim TempString$
Dim TestChar$
Dim TypeOfWindow%
Dim ValueB$
Dim ValueD$
Dim ValueE$
Dim ValueF$
Dim ValueG$
Dim ValueH$
Dim ValueI$
Dim ValueJ$
Dim ValueK$
Dim ValueL$
Dim ValueM$
Dim ValueN$

Dim Found007                 : Found007       = TRUE

Dim i As Integer, p As Integer

Changed      = FALSE
Missing      = FALSE
SR007Count%  = 0
WaltsMacros$ = "[Walt's macros] Extras1:Show007SoundRecs"

' If what is displayed is not a bibliographic record, quit.

TypeOfWindow% = CS.ItemType
Select Case TypeOfWindow%
  Case -1, 3 To 16, 18, 20 To 26
    MsgBox "Sorry, this macro works only in an bibliographic record!", CRITICAL_MESSAGE, WaltsMacros$
    Exit Sub
End Select

' Find all the sound recordings 007 fields and store their values in the array
' "FieldStorage". If any of the mandatory subfields $b, $d, $e, and $g are lacking, set a
' flag to display a warning message in the dialog box.

Instance% = 1
Do Until Found007 = FALSE
  Found007 = CS.GetField( "007", Instance%, FieldData$ )
  If Found007 = TRUE Then
      If Mid$( FieldData$, 6, 1 ) = "s" Then
          ReDim Preserve FieldStorage( SR007Count% )
          FieldStorage( SR007Count% ).tFieldOccurrence = Count007%
          FieldStorage( SR007Count% ).tChanged         = UNCHANGED
          TempString$ = ""

          p = Instr( FieldData$, SUBFIELD_B )
          If p <> 0 Then
              TestChar$ = Mid$( FieldData$, p + 3, 1 )
              If TestChar$ = FILLCHAR Then       'If a fill character is found,
                  TestChar$ = FILLCHAR_X         ' convert it for display purposes.
              End If
              FieldStorage( SR007Count% ).tSMD = TestChar$
            Else
              FieldStorage( SR007Count% ).tSMD = BLANK
              Missing = TRUE
          End If
          TempString$ = TempString$ & FieldStorage( SR007Count% ).tSMD

          p = Instr( FieldData$, SUBFIELD_D )
          If p <> 0 Then
              FieldStorage( SR007Count% ).tSpeed = Mid$( FieldData$, p + 3, 1 )
            Else
              FieldStorage( SR007Count% ).tSpeed = BLANK
              Missing = TRUE
          End If
          TempString$ = TempString$ & FieldStorage( SR007Count% ).tSpeed

          p = Instr( FieldData$, SUBFIELD_E )
          If p <> 0 Then
              FieldStorage( SR007Count% ).tPlaybackChannels = Mid$( FieldData$, p + 3, 1 )
            Else
              FieldStorage( SR007Count% ).tPlaybackChannels = BLANK
              Missing = TRUE
          End If
          TempString$ = TempString$ & FieldStorage( SR007Count% ).tPlaybackChannels

          p = Instr( FieldData$, SUBFIELD_F )
          If p <> 0 Then
              FieldStorage( SR007Count% ).tGroove = Mid$( FieldData$, p + 3, 1 )
            Else
              FieldStorage( SR007Count% ).tGroove = BLANK
          End If
          TempString$ = TempString$ & FieldStorage( SR007Count% ).tGroove

          p = Instr( FieldData$, SUBFIELD_G )
          If p <> 0 Then
              FieldStorage( SR007Count% ).tDimensions = Mid$( FieldData$, p + 3, 1 )
            Else
              FieldStorage( SR007Count% ).tDimensions = BLANK
              Missing = TRUE
          End If
          TempString$ = TempString$ & FieldStorage( SR007Count% ).tDimensions

          p = Instr( FieldData$, SUBFIELD_H )
          If p <> 0 Then
              FieldStorage( SR007Count% ).tTapeWidth = Mid$( FieldData$, p + 3, 1 )
            Else
              FieldStorage( SR007Count% ).tTapeWidth = BLANK
          End If
          TempString$ = TempString$ & FieldStorage( SR007Count% ).tTapeWidth

          p = Instr( FieldData$, SUBFIELD_I )
          If p <> 0 Then
              FieldStorage( SR007Count% ).tTapeConfiguration = Mid$( FieldData$, p + 3, 1 )
            Else
              FieldStorage( SR007Count% ).tTapeConfiguration = BLANK
          End If
          TempString$ = TempString$ & FieldStorage( SR007Count% ).tTapeConfiguration

          p = Instr( FieldData$, SUBFIELD_J )
          If p <> 0 Then
              FieldStorage( SR007Count% ).tKindOfObject = Mid$( FieldData$, p + 3, 1 )
            Else
              FieldStorage( SR007Count% ).tKindOfObject = BLANK
          End If
          TempString$ = TempString$ & FieldStorage( SR007Count% ).tKindOfObject

          p = Instr( FieldData$, SUBFIELD_K )
          If p <> 0 Then
              FieldStorage( SR007Count% ).tKindOfMaterial = Mid$( FieldData$, p + 3, 1 )
            Else
              FieldStorage( SR007Count% ).tKindOfMaterial = BLANK
          End If
          TempString$ = TempString$ & FieldStorage( SR007Count% ).tKindOfMaterial

          p = Instr( FieldData$, SUBFIELD_L )
          If p <> 0 Then
              FieldStorage( SR007Count% ).tKindOfCutting = Mid$( FieldData$, p + 3, 1 )
            Else
              FieldStorage( SR007Count% ).tKindOfCutting = BLANK
          End If
          TempString$ = TempString$ & FieldStorage( SR007Count% ).tKindOfCutting

          p = Instr( FieldData$, SUBFIELD_M )
          If p <> 0 Then
              FieldStorage( SR007Count% ).tPlaybackCharacteristics = Mid$( FieldData$, p + 3, 1 )
            Else
              FieldStorage( SR007Count% ).tPlaybackCharacteristics = BLANK
          End If
          TempString$ = TempString$ & FieldStorage( SR007Count% ).tPlaybackCharacteristics

          p = Instr( FieldData$, SUBFIELD_N )
          If p <> 0 Then
              FieldStorage( SR007Count% ).tCaptureAndStorage = Mid$( FieldData$, p + 3, 1 )
            Else
              FieldStorage( SR007Count% ).tCaptureAndStorage = BLANK
          End If
          TempString$ = TempString$ & FieldStorage( SR007Count% ).tCaptureAndStorage

          FieldStorage( SR007Count% ).tOriginalField = TempString$
          SR007Count% = SR007Count% + 1

      End If
      Count007% = Count007% + 1
  End If
  Instance% = Instance% + 1
Loop

' If no 007 field for a sound recording is found, ask whether to create one. If a new
' field is to be added, fill the array with the default values found in the Sub at the
' end of the macro.

If SR007Count% = 0 Then
    If MsgBox( "No sound recordings 007 field found! Create a new one?", WARNING_QUERY + YES_NO, WaltsMacros$ ) = ANSWER_YES Then
        ReDim FieldStorage( SR007Count% )
        FieldStorage( SR007Count% ).tFieldOccurrence = Count007% + 1
        FieldStorage( SR007Count% ).tChanged         = ADD
        Call SetDefaults( SR007Count% )
        SR007Count% = SR007Count% + 1
      Else
        Exit Sub
    End If
End If

FillArrays

' The main dialog box, showing the present values in the 007 field and offering drop-down
' lists for changing those values, or adding new ones.

SR007Instance% = 1

Begin Dialog Dialog1Definition  300, 276, WaltsMacros, .Dialog1ControlFunction
  PushButton    152, 250,  64,  16, "",                                  .OK
  CancelButton  226, 250,  64,  16
  PushButton    134, 214,  52,  14, "<< &Prev. 007",                     .Prev
  PushButton    222, 214,  52,  14, "&Next 007 >>",                      .Next
  PushButton     12, 250,  64,  16, "&Add new 007",                      .New
  DropListBox   134,  20, 140, 108, Subfield_B_Array(),                  .B
  DropListBox   134,  36, 140, 172, Subfield_D_Array(),                  .D
  DropListBox   134,  52, 140,  72, Subfield_E_Array(),                  .E
  DropListBox   134,  68, 140,  72, Subfield_F_Array(),                  .F
  DropListBox   134,  84, 140, 144, Subfield_G_Array(),                  .G
  DropListBox   134, 100, 140, 120, Subfield_H_Array(),                  .H
  DropListBox   134, 116, 140, 120, Subfield_I_Array(),                  .I
  DropListBox   134, 132, 140, 120, Subfield_J_Array(),                  .J
  DropListBox   134, 148, 140, 144, Subfield_K_Array(),                  .K
  DropListBox   134, 164, 140, 120, Subfield_L_Array(),                  .L
  DropListBox   134, 180, 140, 120, Subfield_M_Array(),                  .M
  DropListBox   134, 196, 140, 120, Subfield_N_Array(),                  .N
  Text            8,   8,  40,   8, "Subfield"
  Text            8,  22,   4,   8, "*",                                 .MandatoryAsterisk1
  Text           12,  22,   8,   8, "b"
  Text           22,  22, 112,   8, "Specific material designation:"
  Text          278,  22,  16,   8, "/01"
  Text            8,  38,   4,   8, "*",                                 .MandatoryAsterisk2
  Text           12,  38,   8,   8, "d"
  Text           22,  38, 112,   8, "Speed:"
  Text          278,  38,  16,   8, "/03"
  Text            8,  54,   4,   8, "*",                                 .MandatoryAsterisk3
  Text           12,  54,   8,   8, "e"
  Text           22,  54, 112,   8, "Config. of playback channels:"
  Text          278,  54,  16,   8, "/04"
  Text           12,  70,   8,   8, "f"
  Text           22,  70, 112,   8, "Groove width/groove pitch:"
  Text          278,  70,  16,   8, "/05"
  Text            8,  86,   4,   8, "*",                                 .MandatoryAsterisk4
  Text           12,  86,   8,   8, "g"
  Text           22,  86, 112,   8, "Dimensions:"
  Text          278,  86,  16,   8, "/06"
  Text           12, 102,   8,   8, "h"
  Text           22, 102, 112,   8, "Tape width:"
  Text          278, 102,  16,   8, "/07"
  Text           12, 118,   8,   8, "i"
  Text           22, 118, 112,   8, "Tape configuration:"
  Text          278, 118,  16,   8, "/08"
  Text           12, 134,   8,   8, "j"
  Text           22, 134, 112,   8, "Kind of disc, cylinder or tape:"
  Text          278, 134,  16,   8, "/09"
  Text           12, 150,   8,   8, "k"
  Text           22, 150, 112,   8, "Kind of material:"
  Text          278, 150,  16,   8, "/10"
  Text           12, 166,   8,   8, "l"
  Text           22, 166, 112,   8, "Kind of cutting:"
  Text          278, 166,  16,   8, "/11"
  Text           12, 182,   8,   8, "m"
  Text           22, 182, 112,   8, "Special playback characteristics:"
  Text          278, 182,  16,   8, "/12"
  Text           12, 198,   8,   8, "m"
  Text           22, 198, 112,   8, "Capture and storage:"
  Text          278, 198,  16,   8, "/13"
  Text           12, 214, 120,  16, MandatoryText$,                      .MandatoryNote
  Text          170, 232,  88,   8, "",                                  .FieldManager
End Dialog

Dim Dialog1 As Dialog1Definition

If Dialog( Dialog1 ) = DIALOG_BUTTON_CANCEL Then Exit Sub

' If a change has been detected, construct a new 007 string by reading the values of the
' list boxes when the OK button was clicked and adding them all together. If an element
' is blank, don't bother adding it to the string.

For i = 0 To SR007Count% - 1

  If FieldStorage( i ).tChanged <> UNCHANGED Then
      ValueB$     = FieldStorage( i ).tSMD
      If ValueB$ = FILLCHAR_X Then ValueB$ = FILLCHAR
      ValueD$     = FieldStorage( i ).tSpeed
      ValueE$     = FieldStorage( i ).tPlaybackChannels
      ValueF$     = FieldStorage( i ).tGroove
      ValueG$     = FieldStorage( i ).tDimensions
      ValueH$     = FieldStorage( i ).tTapeWidth
      ValueI$     = FieldStorage( i ).tTapeConfiguration
      ValueJ$     = FieldStorage( i ).tKindOfObject
      ValueK$     = FieldStorage( i ).tKindOfMaterial
      ValueL$     = FieldStorage( i ).tKindOfCutting
      ValueM$     = FieldStorage( i ).tPlaybackCharacteristics
      ValueN$     = FieldStorage( i ).tCaptureAndStorage
      TempString$ = ValueB$ & ValueD$ & ValueE$ & ValueF$ & ValueG$ & ValueH$ & ValueI$ & ValueJ$ & ValueK$ & ValueL$ & ValueM$ & ValueN$

' Although the storage variable contains an indication of a change having been made to
' the field, the change could have been reversed, so compare the current values with the
' original values to verify that a change actually occurred.

      If TempString$ <> FieldStorage( i ).tOriginalField Then
          Field007String$ = "007  s " & SUBFIELD_B & BLANK & ValueB$ & BLANK & SUBFIELD_D & BLANK & ValueD$ & BLANK & SUBFIELD_E & BLANK & ValueE$

          If ValueF$ <> BLANK Then
              Field007String$ = Field007String$ & BLANK & SUBFIELD_F & BLANK & ValueF$
          End If

          Field007String$ = Field007String$ & BLANK & SUBFIELD_G & BLANK & ValueG$

          If ValueH$ <> BLANK Then
              Field007String$ = Field007String$ & BLANK & SUBFIELD_H & BLANK & ValueH$
          End If

          If ValueI$ <> BLANK Then
              Field007String$ = Field007String$ & BLANK & SUBFIELD_I & BLANK & ValueI$
          End If

          If ValueJ$ <> BLANK Then
              Field007String$ = Field007String$ & BLANK & SUBFIELD_J & BLANK & ValueJ$
          End If

          If ValueK$ <> BLANK Then
              Field007String$ = Field007String$ & BLANK & SUBFIELD_K & BLANK & ValueK$
          End If

          If ValueL$ <> BLANK Then
              Field007String$ = Field007String$ & BLANK & SUBFIELD_L & BLANK & ValueL$
          End If

          If ValueM$ <> BLANK Then
              Field007String$ = Field007String$ & BLANK & SUBFIELD_M & BLANK & ValueM$
          End If

          If ValueN$ <> BLANK Then
              Field007String$ = Field007String$ & BLANK & SUBFIELD_N & BLANK & ValueN$
          End If

' Add a new field after all existing sound recordings 007 fields. Otherwise, a changed
' field replaces an existing one.

          FieldPosition% = FieldStorage( i ).tFieldOccurrence + 1
          If FieldStorage( i ).tChanged = ADD Then
              If CS.AddField( FieldPosition%, Field007String$ ) Then
                  Added% = Added% + 1
                Else
                  MsgBox "Sorry, could not add 007.", CRITICAL_MESSAGE, WaltsMacros$
                  Exit Sub
              End If
            ElseIf FieldStorage( i ).tChanged = REPLACE Then
              If CS.SetField( FieldPosition%, Field007String$ ) Then
                  Replaced% = Replaced% + 1
                Else
                  MsgBox "Sorry, could not replace 007.", CRITICAL_MESSAGE, WaltsMacros$
                  Exit Sub
              End If
          End If
      End If
  End If
Next i

' Display a message about added and replaced fields.

If Added% = 0 And Replaced% = 0 Then
    Exit Sub
  ElseIf Added% = 1 And Replaced% = 0 Then
    ConcludingMessage$ = "A new 007 field was added."
  ElseIf Added% > 1 And Replaced% = 0 Then
    ConcludingMessage$ = "New 007 fields were added."
  ElseIf Added% = 0 And Replaced% = 1 Then
    If SR007Count% = 1 Then
        ConcludingMessage$ = "The 007 field was replaced."
      Else
        ConcludingMessage$ = "One 007 field was replaced."
    End If
  ElseIf Added% = 1 And Replaced% = 1 Then
    ConcludingMessage$ = "A new 007 field was added and one 007 field was replaced."
  ElseIf Added% = 1 And Replaced% > 1 Then
    ConcludingMessage$ = "A new 007 field was added and " & Trim$( Str$( Replaced% ) ) & " 007 fields were replaced."
  ElseIf Added% > 1 And Replaced% > 1 Then
    ConcludingMessage$ = "New 007 fields were added and " & Trim$( Str$( Replaced% ) ) & " 007 fields were replaced."
End If

MsgBox ConcludingMessage$, INFORMATION_MESSAGE, WaltsMacros$

End Sub

'****************************************************************************************

Sub FillArrays

' This sub fills the arrays presented by the dialog box as the available options for each
' subfield. A space, or blank, appears as the first element in some arrays and as the
' last in others. In optional subfields, it signals no attempt to code; a blank subfield
' will simply not appear in the final, formatted field. It can be the first element of an
' array because it is valid, and sometimes an appropriate choice. On the other hand, for
' mandatory subfields, the space is the last element of the array. Because it is not a
' valid option, its placement below all the other values may mean it is less likely to be
' selected by accident.

Subfield_B_Array( 0 )  = "d - Sound disc"
Subfield_B_Array( 1 )  = "e - Cylinder"
Subfield_B_Array( 2 )  = "g - Sound cartridge"
Subfield_B_Array( 3 )  = "i - Sound-track film"
Subfield_B_Array( 4 )  = "q - Roll"
Subfield_B_Array( 5 )  = "s - Sound cassette"
Subfield_B_Array( 6 )  = "t - Sound-tape reel"
Subfield_B_Array( 7 )  = "u - Unspecified"
Subfield_B_Array( 8 )  = "w - Wire recording"
Subfield_B_Array( 9 )  = "z - Other"
Subfield_B_Array( 10 ) = Chr$( 127 ) & " - No attempt made to code"
Subfield_B_Array( 11 ) = BLANK

Subfield_D_Array( 0 )  = "a - 16 rpm [analog disc]"
Subfield_D_Array( 1 )  = "b - 33-1/3 rpm [analog disc]"
Subfield_D_Array( 2 )  = "c - 45 rpm [analog disc]"
Subfield_D_Array( 3 )  = "d - 78 rpm [analog disc]"
Subfield_D_Array( 4 )  = "e - 8 rpm [analog disc]"
Subfield_D_Array( 5 )  = "f - 1.4 m. per second [compact disc]"
Subfield_D_Array( 6 )  = "h - 120 rpm [cylinders]"
Subfield_D_Array( 7 )  = "i - 160 rpm [cylinders]"
Subfield_D_Array( 8 )  = "k - 15/16 ips [tapes]"
Subfield_D_Array( 9 )  = "l - 1-7/8 ips [tapes]"
Subfield_D_Array( 10 ) = "m - 3-3/4 ips [tapes]"
Subfield_D_Array( 11 ) = "o - 7-1/2 ips [tapes]"
Subfield_D_Array( 12 ) = "p - 15 ips [tapes]"
Subfield_D_Array( 13 ) = "r - 30 ips [tapes]"
Subfield_D_Array( 14 ) = "u - Unknown"
Subfield_D_Array( 15 ) = "z - Other"
Subfield_D_Array( 16 ) = BLANK

Subfield_E_Array( 0 )  = "m - Monaural"
Subfield_E_Array( 1 )  = "q - Quadraphonic, multichannel, or surround"
Subfield_E_Array( 2 )  = "s - Stereophonic"
Subfield_E_Array( 3 )  = "u - Unknown"
Subfield_E_Array( 4 )  = "z - Other"
Subfield_E_Array( 5 )  = BLANK

Subfield_F_Array( 0 )  = BLANK
Subfield_F_Array( 1 )  = "m - Microgroove/fine"
Subfield_F_Array( 2 )  = "n - Not applicable"
Subfield_F_Array( 3 )  = "s - Coarse/standard"
Subfield_F_Array( 4 )  = "u - Unknown"
Subfield_F_Array( 5 )  = "z - Other"

Subfield_G_Array( 0 )  = "a - 3 in."
Subfield_G_Array( 1 )  = "b - 5 in."
Subfield_G_Array( 2 )  = "c - 7 in."
Subfield_G_Array( 3 )  = "d - 10 in."
Subfield_G_Array( 4 )  = "e - 12 in."
Subfield_G_Array( 5 )  = "f - 16 in."
Subfield_G_Array( 6 )  = "g - 4-3/4 in. ( 12 cm )"
Subfield_G_Array( 7 )  = "j - 3-7/8 x 2-1/2 in. [cassette]"
Subfield_G_Array( 8 )  = "o - 5-1/4 x 3-7/8 in. [cartridge]"
Subfield_G_Array( 9 )  = "s - 2-3/4 x 4 in. [cylinder]"
Subfield_G_Array( 10 ) = "n - Not applicable"
Subfield_G_Array( 11 ) = "u - Unknown"
Subfield_G_Array( 12 ) = "z - Other"
Subfield_G_Array( 13 ) = BLANK

Subfield_H_Array( 0 )  = BLANK
Subfield_H_Array( 1 )  = "l - 1/8 in."
Subfield_H_Array( 2 )  = "m - 1/4 in."
Subfield_H_Array( 3 )  = "n - Not applicable"
Subfield_H_Array( 4 )  = "o - 1/2 in."
Subfield_H_Array( 5 )  = "p - 1 in."
Subfield_H_Array( 6 )  = "u - Unknown"
Subfield_H_Array( 7 )  = "z - Other"

Subfield_I_Array( 0 )  = BLANK
Subfield_I_Array( 1 )  = "a - Full ( 1 ) track"
Subfield_I_Array( 2 )  = "b - Half ( 2 ) track"
Subfield_I_Array( 3 )  = "c - Quarter ( 4 ) track"
Subfield_I_Array( 4 )  = "d - 8 track"
Subfield_I_Array( 5 )  = "e - 12 track"
Subfield_I_Array( 6 )  = "f - 16 track"
Subfield_I_Array( 7 )  = "n - Not applicable"
Subfield_I_Array( 8 )  = "u - Unknown"
Subfield_I_Array( 9 )  = "z - Other"

Subfield_J_Array( 0 )  = BLANK
Subfield_J_Array( 1 )  = "a - Master tape"
Subfield_J_Array( 2 )  = "b - Tape duplication master"
Subfield_J_Array( 3 )  = "d - Disc master (negative)"
Subfield_J_Array( 4 )  = "i - Instantaneous (recorded on the spot)"
Subfield_J_Array( 5 )  = "m - Mass produced"
Subfield_J_Array( 6 )  = "n - Not applicable"
Subfield_J_Array( 7 )  = "r - Mother (positive)"
Subfield_J_Array( 8 )  = "s - Stamper (negative)"
Subfield_J_Array( 9 )  = "t - Test pressing"
Subfield_J_Array( 10 ) = "u - Unknown"
Subfield_J_Array( 11 ) = "z - Other"

Subfield_K_Array( 0 )  = BLANK
Subfield_K_Array( 1 )  = "a - Lacquer coating"
Subfield_K_Array( 2 )  = "b - Cellulose nitrate"
Subfield_K_Array( 3 )  = "c - Acetate tape with ferrous oxide"
Subfield_K_Array( 4 )  = "g - Glass with lacquer"
Subfield_K_Array( 5 )  = "i - Aluminum with lacquer"
Subfield_K_Array( 6 )  = "l - Metal"
Subfield_K_Array( 7 )  = "m - Plastic with metal"
Subfield_K_Array( 8 )  = "p - Plastic"
Subfield_K_Array( 9 )  = "r - Paper with lacquer or ferrous oxide"
Subfield_K_Array( 10 ) = "s - Shellac"
Subfield_K_Array( 11 ) = "u - Unknown"
Subfield_K_Array( 12 ) = "w - Wax"
Subfield_K_Array( 13 ) = "z - Other"

Subfield_L_Array( 0 )  = BLANK
Subfield_L_Array( 1 )  = "h - Hill-and-dale cutting"
Subfield_L_Array( 2 )  = "l - Lateral or combined cutting"
Subfield_L_Array( 3 )  = "n - Not applicable"
Subfield_L_Array( 4 )  = "u - Unknown"

Subfield_M_Array( 0 )  = BLANK
Subfield_M_Array( 1 )  = "a - NAB standard"
Subfield_M_Array( 2 )  = "b - CCIR standard"
Subfield_M_Array( 3 )  = "c - Dolby-B encoded"
Subfield_M_Array( 4 )  = "d - dbx encoded"
Subfield_M_Array( 5 )  = "e - Digital recording"
Subfield_M_Array( 6 )  = "f - Dolby-A encoded"
Subfield_M_Array( 7 )  = "g - Dolby-C encoded"
Subfield_M_Array( 8 )  = "h - CX encoded"
Subfield_M_Array( 9 )  = "n - Not applicable"
Subfield_M_Array( 10 ) = "u - Unknown"
Subfield_M_Array( 11 ) = "z - Other"

Subfield_N_Array( 0 )  = BLANK
Subfield_N_Array( 1 )  = "a - Acoustical capture, direct storage"
Subfield_N_Array( 2 )  = "b - Direct storage, not acoustical"
Subfield_N_Array( 3 )  = "d - Digital storage"
Subfield_N_Array( 4 )  = "e - Analog electrical storage"
Subfield_N_Array( 5 )  = "u - Unknown"
Subfield_N_Array( 6 )  = "z - Other"

End Sub

'****************************************************************************************

Sub SetDefaults( Which007% )

' These are the default codes when a new 007 field is to be added. They are for compact
' discs; change them as appropriate for other formats, or for more suitable values.

FieldStorage( Which007% ).tSMD                     = "d"    'MANDATORY
FieldStorage( Which007% ).tSpeed                   = "f"    'MANDATORY
FieldStorage( Which007% ).tPlaybackChannels        = "u"    'MANDATORY
FieldStorage( Which007% ).tGroove                  = "n"    'OPTIONAL
FieldStorage( Which007% ).tDimensions              = "g"    'MANDATORY
FieldStorage( Which007% ).tTapeWidth               = "n"    'REQUIRED IF APPLICABLE
FieldStorage( Which007% ).tTapeConfiguration       = "n"    'REQUIRED IF APPLICABLE
FieldStorage( Which007% ).tKindOfObject            = BLANK  'OPTIONAL
FieldStorage( Which007% ).tKindOfMaterial          = BLANK  'OPTIONAL
FieldStorage( Which007% ).tKindOfCutting           = BLANK  'OPTIONAL
FieldStorage( Which007% ).tPlaybackCharacteristics = "e"    'OPTIONAL
FieldStorage( Which007% ).tCaptureAndStorage       = "d"    'OPTIONAL

End Sub

'****************************************************************************************

Sub SetListValues( Which007% )

' This sub sets the selection values of the lists in the dialog box (that is, which
' entry in the list should be displayed in the drop-down box).

DlgValue   "B",                  ListSelection( Which007%, "SMD" )
DlgValue   "D",                  ListSelection( Which007%, "Speed" )
DlgValue   "E",                  ListSelection( Which007%, "PlaybackChannels" )
DlgValue   "F",                  ListSelection( Which007%, "Groove" )
DlgValue   "G",                  ListSelection( Which007%, "Dimensions" )
DlgValue   "H",                  ListSelection( Which007%, "TapeWidth" )
DlgValue   "I",                  ListSelection( Which007%, "TapeConfiguration" )
DlgValue   "J",                  ListSelection( Which007%, "KindOfObject" )
DlgValue   "K",                  ListSelection( Which007%, "KindOfMaterial" )
DlgValue   "L",                  ListSelection( Which007%, "KindOfCutting" )
DlgValue   "M",                  ListSelection( Which007%, "PlaybackCharacteristics" )
DlgValue   "N",                  ListSelection( Which007%, "CaptureAndStorage" )

End Sub

'****************************************************************************************

Function ListSelection( Which007%, WhichField$ ) As Integer

' This function, given a specific 007 field and the field of the "FieldContents"
' variable, returns the index number of the selected code in the array for the
' appropriate subfield. It does this by finding the code in that variable and comparing
' it to the possible values in the array. When it finds a match, that index number is the
' return value of the function.

Dim Code$

Dim i As Integer

Select Case WhichField$

  Case "SMD"
    Code$ = FieldStorage( Which007% ).tSMD
    For i = 0 To 11
      If Code$ = Left$( Subfield_B_Array( i ), 1 ) Then
          ListSelection = i
          Exit Function
      End If
    Next i

  Case "Speed"
    Code$ = FieldStorage( Which007% ).tSpeed
    For i = 0 To 16
      If Code$ = Left$( Subfield_D_Array( i ), 1 ) Then
          ListSelection = i
          Exit Function
      End If
    Next i

  Case "PlaybackChannels"
    Code$ = FieldStorage( Which007% ).tPlaybackChannels
    For i = 0 To 5
      If Code$ = Left$( Subfield_E_Array( i ), 1 ) Then
          ListSelection = i
          Exit Function
      End If
    Next i

  Case "Groove"
    Code$ = FieldStorage( Which007% ).tGroove
    For i = 0 To 5
      If Code$ = Left$( Subfield_F_Array( i ), 1 ) Then
          ListSelection = i
          Exit Function
      End If
    Next i

  Case "Dimensions"
    Code$ = FieldStorage( Which007% ).tDimensions
    For i = 0 To 13
      If Code$ = Left$( Subfield_G_Array( i ), 1 ) Then
          ListSelection = i
          Exit Function
      End If
    Next i

  Case "TapeWidth"
    Code$ = FieldStorage( Which007% ).tTapeWidth
    For i = 0 To 7
      If Code$ = Left$( Subfield_H_Array( i ), 1 ) Then
          ListSelection = i
          Exit Function
      End If
    Next i

  Case "TapeConfiguration"
    Code$ = FieldStorage( Which007% ).tTapeConfiguration
    For i = 0 To 9
      If Code$ = Left$( Subfield_I_Array( i ), 1 ) Then
          ListSelection = i
          Exit Function
      End If
    Next i

  Case "KindOfObject"
    Code$ = FieldStorage( Which007% ).tKindOfObject
    For i = 0 To 11
      If Code$ = Left$( Subfield_J_Array( i ), 1 ) Then
          ListSelection = i
          Exit Function
      End If
    Next i

  Case "KindOfMaterial"
    Code$ = FieldStorage( Which007% ).tKindOfMaterial
    For i = 0 To 13
      If Code$ = Left$( Subfield_K_Array( i ), 1 ) Then
          ListSelection = i
          Exit Function
      End If
    Next i

  Case "KindOfCutting"
    Code$ = FieldStorage( Which007% ).tKindOfCutting
    For i = 0 To 4
      If Code$ = Left$( Subfield_L_Array( i ), 1 ) Then
          ListSelection = i
          Exit Function
      End If
    Next i

  Case "PlaybackCharacteristics"
    Code$ = FieldStorage( Which007% ).tPlaybackCharacteristics
    For i = 0 To 11
      If Code$ = Left$( Subfield_M_Array( i ), 1 ) Then
          ListSelection = i
          Exit Function
      End If
    Next i

  Case "CaptureAndStorage"
    Code$ = FieldStorage( Which007% ).tCaptureAndStorage
    For i = 0 To 6
      If Code$ = Left$( Subfield_N_Array( i ), 1 ) Then
          ListSelection = i
          Exit Function
      End If
    Next i

End Select

End Function

'****************************************************************************************

Function Dialog1ControlFunction( Id$, Action%, SVal& )

' The place where all the action happens.

Const CONTROL_CHANGE  As Integer = 2   'The value of the dialog box function parameter "Action%" when a control changes.
Const DISABLED        As Integer = 0   'The value to make a dialog box control disabled.
Const ENABLED         As Integer = 1   'The value to make a dialog box control enabled.
Const INITIALIZE      As Integer = 1   'The value of the dialog box function parameter "Action%" when the dialog box opens.
Const INVISIBLE       As Integer = 0   'The value to make a dialog box control invisible.
Const KEEP_DLG_OPEN   As Integer = -1  'The value of the dialog box function to keep a dialog box open.
Const VISIBLE         As Integer = 1   'The value to make a dialog box control visible.
Const WARNING_MESSAGE As Integer = 48  'The value to display the "Warning message" icon in a message box.

Select Case Action%

  Case INITIALIZE

' Display of the "Prev" and "Next" buttons depends on how many sound recording 007 fields
' there are. If a new field is in the process of being added, indicate that by showing
' "new" after the number of the field. Such a field will always be the last one in the
' group of 007 fields.

AddNew:

    If SR007Count% < 2 Then
        DlgVisible "FieldManager",       INVISIBLE
        DlgVisible "Next",               INVISIBLE
        DlgVisible "Prev",               INVISIBLE
      Else
        DlgVisible "FieldManager",       VISIBLE
        If SR007Instance% = NewField% Then
            DlgText    "FieldManager",       "Field " & Trim$( Str$( SR007Instance% ) ) & " (new) of " & Trim$( Str$( SR007Count% ) ) & " shown."
          Else
            DlgText    "FieldManager",       "Field " & Trim$( Str$( SR007Instance% ) ) & " of " & Trim$( Str$( SR007Count% ) ) & " shown."
        End If
        DlgVisible "Next",               VISIBLE
        DlgVisible "Prev",               VISIBLE
        If SR007Instance% = 1 Then
            DlgEnable  "Next",               ENABLED
            DlgEnable  "Prev",               DISABLED
          ElseIf SR007Instance% = SR007Count% Then
            DlgEnable  "Next",               DISABLED
            DlgEnable  "Prev",               ENABLED
          Else
            DlgEnable  "Next",               ENABLED
            DlgEnable  "Prev",               ENABLED
        End If
    End If

' If there are no 007 sound recording fields, hide the "Add new field" button, because
' the macro is adding a new field anyway. Or, if the "New" button has just been clicked,
' hide it because the field it is going to add is being edited. But if an existing 007
' field is being displayed for editing, then the option to add a new field via the "New"
' button must be available.

    If FieldStorage( SR007Count% - 1 ).tChanged = ADD Then
        DlgVisible "New",                INVISIBLE
        DlgText    "OK",                 "Add 007"
      Else
        DlgEnable  "New",                VISIBLE
        DlgText    "OK",                 "Close"
    End If

' When the macro opens, the first 007 field (whether it's being added, or it already
' exists) is always displayed. If a new field (in addition to any existing fields) is
' being added, it's the last in the group, and is the one shown.

    If NewField% = 0 Then
        Call SetListValues( 0 )
      Else
        Call SetListValues( SR007Count% - 1 )
    End If

' If an existing 007 field lacks mandatory subfields, display in the dialog box the
' warning message and the asterisks to highlight the mandatory subfields.

    If Missing = TRUE Then
        DlgVisible "MandatoryNote",      VISIBLE
        DlgVisible "MandatoryAsterisk1", VISIBLE
        DlgVisible "MandatoryAsterisk2", VISIBLE
        DlgVisible "MandatoryAsterisk3", VISIBLE
        DlgVisible "MandatoryAsterisk4", VISIBLE
      Else
        DlgVisible "MandatoryNote",      INVISIBLE
        DlgVisible "MandatoryAsterisk1", INVISIBLE
        DlgVisible "MandatoryAsterisk2", INVISIBLE
        DlgVisible "MandatoryAsterisk3", INVISIBLE
        DlgVisible "MandatoryAsterisk4", INVISIBLE
    End If

    Case CONTROL_CHANGE

      Select Case Id$

' If a new field is to be added, enlarge the array. Set the flag to show that the field
' is a new one to be added. Increment the 007 field count so it gets added after any
' existing 007 fields. Make this new field the one to display in the dialog box, and
' populate it with the default values.

        Case "New"
          ReDim Preserve FieldStorage( SR007Count% )
          FieldStorage( SR007Count% ).tChanged = ADD
          FieldStorage( SR007Count% ).tFieldOccurrence = FieldStorage( SR007Count% - 1 ).tFieldOccurrence + 1
          Call SetDefaults( SR007Count% )
          Call SetListValues( SR007Count% )
          SR007Count%            = SR007Count% + 1
          SR007Instance%         = SR007Count%
          DlgEnable  "New",                DISABLED
          NewField%              = SR007Instance%
          Dialog1ControlFunction = KEEP_DLG_OPEN
          GoTo AddNew:

' The Next and Prev buttons navigate through multiple 007 fields.

        Case "Next", "Prev"
          If Id$ = "Next" Then
              SR007Instance% = SR007Instance% + 1
              DlgEnable  "Prev",               ENABLED
              If SR007Instance% = SR007Count% Then
                  DlgEnable  "Next",               DISABLED
              End If
            Else
              SR007Instance% = SR007Instance% - 1
              DlgEnable  "Next",               ENABLED
              If SR007Instance% = 1 Then
                  DlgEnable  "Prev",               DISABLED
              End If
          End If

' Indicate if a field is being newly added by adding the word "new" after its occurrence.
' A newly added field always appears following the existing fields.

          If SR007Instance% = NewField% Then
              DlgText    "FieldManager",       "Field " & Trim$( Str$( SR007Instance% ) ) & " (new) of " & Trim$( Str$( SR007Count% ) ) & " shown."
            Else
              DlgText    "FieldManager",       "Field " & Trim$( Str$( SR007Instance% ) ) & " of " & Trim$( Str$( SR007Count% ) ) & " shown."
          End If
          Call SetListValues( SR007Instance% - 1 )
          Dialog1ControlFunction = KEEP_DLG_OPEN

' If some mandatory subfields have no values selected when the OK button is clicked, a
' warning must show and the dialog box stay open to accept a fix.

        Case "OK"
          If DlgText( "B" ) = BLANK Or DlgText( "D" ) = BLANK Or DlgText( "E" ) = BLANK Or DlgText( "G" ) = BLANK Then
              MsgBox "One or more mandatory subfields requires appropriate codes! Please select appropriate values to add to those subfields.", WARNING_MESSAGE, WaltsMacros$
              Dialog1ControlFunction = KEEP_DLG_OPEN
          End If

' If the "Cancel" button was clicked but changes have been recorded, confirm that the
' intent is to quit the macro.

        Case "Cancel"
          If Changed Then
              If MsgBox( "Do you want to abandon your changes and stop the macro?", WARNING_QUERY + YES_NO, WaltsMacros$ ) = ANSWER_YES Then
                  DlgEnd     0
                Else
                  Dialog1ControlFunction = KEEP_DLG_OPEN
              End If
          End If

' When a value in a list box changes, update the Contents array, and set the flag to mark
' that a change was made.

        Case "B"
          FieldStorage( SR007Instance% - 1 ).tSMD = Left$( Subfield_B_Array( SVal& ), 1 )
          Changed = TRUE

        Case "D"
          FieldStorage( SR007Instance% - 1 ).tSpeed = Left$( Subfield_D_Array( SVal& ), 1 )
          Changed = TRUE

        Case "E"
          FieldStorage( SR007Instance% - 1 ).tPlaybackChannels = Left$( Subfield_E_Array( SVal& ), 1 )
          Changed = TRUE

        Case "F"
          FieldStorage( SR007Instance% - 1 ).tGroove = Left$( Subfield_F_Array( SVal& ), 1 )
          Changed = TRUE

        Case "G"
          FieldStorage( SR007Instance% - 1 ).tDimensions = Left$( Subfield_G_Array( SVal& ), 1 )
          Changed = TRUE

        Case "H"
          FieldStorage( SR007Instance% - 1 ).tTapeWidth = Left$( Subfield_H_Array( SVal& ), 1 )
          Changed = TRUE

        Case "I"
          FieldStorage( SR007Instance% - 1 ).tTapeConfiguration = Left$( Subfield_I_Array( SVal& ), 1 )
          Changed = TRUE

        Case "J"
          FieldStorage( SR007Instance% - 1 ).tKindOfObject = Left$( Subfield_J_Array( SVal& ), 1 )
          Changed = TRUE

        Case "K"
          FieldStorage( SR007Instance% - 1 ).tKindOfMaterial = Left$( Subfield_K_Array( SVal& ), 1 )
          Changed = TRUE

        Case "L"
          FieldStorage( SR007Instance% - 1 ).tKindOfCutting = Left$( Subfield_L_Array( SVal& ), 1 )
          Changed = TRUE

        Case "M"
          FieldStorage( SR007Instance% - 1 ).tPlaybackCharacteristics = Left$( Subfield_M_Array( SVal& ), 1 )
          Changed = TRUE

        Case "N"
          FieldStorage( SR007Instance% - 1 ).tCaptureAndStorage = Left$( Subfield_N_Array( SVal& ), 1 )
          Changed = TRUE

      End Select

      If Changed Then
          If FieldStorage( SR007Instance% - 1 ).tChanged = UNCHANGED Then
              FieldStorage( SR007Instance% - 1 ).tChanged = REPLACE
          End If
          DlgText    "OK",                 "Change 007"
      End If

End Select

End Function
'104243026
