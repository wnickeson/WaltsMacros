'MacroName:FormatCopied6XX.2022.04
'MacroDescription:Formats subject fields copied from outside Connexion, such as from a
' Web OPAC, changing dashes to delimiters and adding some subfield codes.
'
' This macro was written by Walter F. Nickeson,
' University of Rochester, Rochester, NY
' wnickeson@library.rochester.edu
'
' Last updated: 29 June 2022.
' Check for the latest versions of this and my other macros at
' https://github.com/wnickeson/WaltsMacros
' Please e-mail me with bug reports or to suggest improvements.
'
' This macro works for me with Connexion client 3.1 and 64-bit Windows 10 Enterprise &
' 64-bit Windows 10 Pro, but no guarantees are promised or implied.
'
'****************************************************************************************
' How it works: This macro formats subject headings pasted into Connexion from a non-MARC
' source, such as from the Web. It changes dashes to delimiters, with a standard subfield
' code of "x" or "y", and it changes the pipe symbol ("|") and dollar sign ("$") to the
' standard OCLC delimiter.
'
' Place the cursor in the field, which must already be tagged as a subject, and run the
' macro. For example, it changes this field, copied directly from LC's online catalog
' (with tagging supplied manually, and note that the delimiter doesn't display in this
' context, so the dollar sign is used):
'
'   650   0 Poets, Irish--20th century--Biography.
'
' to this:
'
'   650   0 Poets, Irish $y 20th century $x Biography.
'
' If dashes are replaced, the macro displays a warning message to check subfield coding,
' as of course it cannot tell whether any particular subfield should be "v," "x," "y," or
' "z" (although if it finds that the first character in a subfield is a digit, the macro
' will assume the subfield is a chronological subdivision and will code it "y").
'
' Replacing the pipe symbol is simple and straightforward.
'****************************************************************************************

Option Explicit

'****************************************************************************************

Sub Main

Dim CS As Object
On Error Resume Next
Set CS = GetObject( , "Connex.Client" )
On Error GoTo 0
If CS Is Nothing Then Set CS = CreateObject( "Connex.Client" )

Const CRITICAL_MESSAGE    As Integer = 16
Const INFORMATION_MESSAGE As Integer = 64

Dim FieldData$
Dim NextChar%
Dim Row%
Dim Start%
Dim SubfieldCode$
Dim SubjectString$
Dim Tag$
Dim TypeOfWindow%
Dim WaltsMacros$             : WaltsMacros$ = "[Walt's macros] Extras2:FormatCopied6XX"

Dim Changed                  : Changed      = FALSE
Dim Dashes                   : Dashes       = FALSE

Dim p As Integer

Row% = CS.CursorRow

' First, make sure a bibliographic record is displayed.

TypeOfWindow% = CS.ItemType
Select Case TypeOfWindow%
  Case -1, 3 To 16, 18, 20 To 26
    MsgBox "Sorry, this macro works only in a bibliographic record!", CRITICAL_MESSAGE, WaltsMacros$
    Exit Sub
End Select

' Then, get the data in the row in which the cursor is placed, and check that the field
' is tagged as a subject; this serves as a reminder to examine the tagging.

If CS.GetFieldLine( Row%, FieldData$ ) = TRUE Then
    Tag$ = Left$( FieldData$, 5 )
    If Left$( FieldData$, 1 ) = "6" Then
        SubjectString$ = Mid$( FieldData$, 6 )
      Else
        MsgBox "This macro works only in fields tagged as subjects (6XX).", CRITICAL_MESSAGE, WaltsMacros$
        GoTo Done:
    End If
End If

' Then check for presence of dashes. First, see if there are dashes surrounded by spaces,
' and if there are, remove them.

Do
  p = InStr( SubjectString$, " -- " )
  If p <> 0 Then
      SubjectString$ = Left$( SubjectString$, p - 1 ) & "--" & Mid$( SubjectString$, p + 4 )
    Else
      Exit Do
  End If
Loop Until p = 0

' Now replace dashes with delimiters.

Do
  p = InStr( SubjectString$, "--" )
  If p <> 0 Then

' If a dash is found, check to see if the first character following is a digit; if it is,
' assume the subfield is a chronological subdivision, and make the subfield code "y";
' otherwise, make it "x".

      NextChar% = Asc( Mid$( SubjectString$, p + 2, 1 ) )
      If NextChar% > 47 And NextChar% < 58 Then
          SubfieldCode$ = "y "
        Else
          SubfieldCode$ = "x "
      End If

      SubjectString$ = Left$( SubjectString$, p - 1 ) & " " & Chr$( 223 ) & SubfieldCode$ & Mid$( SubjectString$, p + 2 )
      Changed = TRUE
      Dashes = TRUE
    Else
      If Changed = TRUE Then
          Exit Do
        Else
          GoTo Pipe:
      End If
  End If
Loop Until p = 0

Pipe:

' Change pipes to delimiters.

Do
  p = InStr( SubjectString$, Chr$( 124 ) )
  If p <> 0 Then
      SubjectString$ = Left$( SubjectString$, p - 1 ) & Chr$( 223 ) & Mid$( SubjectString$, p + 1)
      Changed = TRUE
    Else
      GoTo Dollar:
  End If
Loop until p = 0

Dollar:

' Change dollar signs to delimiters.

Do
  p = InStr( SubjectString$, Chr$( 36 ) )
  If p <> 0 Then
      SubjectString$ = Left$( SubjectString$, p - 1 ) & Chr$( 223 ) & Mid$( SubjectString$, p + 1)
      Changed = TRUE
    Else
      If Changed = FALSE Then
          MsgBox "Nothing was changed!", INFORMATION_MESSAGE, WaltsMacros$
          Exit Do
      End If
  End If
Loop until p = 0

' Now add the altered field.

SubjectString$ = Tag$ & SubjectString$
If CS.AddFieldLine( Row%, SubjectString$ ) = FALSE Then
    MsgBox "Sorry, could not format subject field.", CRITICAL_MESSAGE, WaltsMacros$
  Else
    If Dashes = TRUE Then MsgBox "Check subfield coding!", INFORMATION_MESSAGE, WaltsMacros$
End If

Done:

End Sub
'1815777
'
'Macro name: FormatCopied6XX
'Macro book: C:\Users\wnickeson.UR\AppData\Roaming\OCLC\Connex\Macros\Extras2.mbk
'Saved: 8/22/2023 10:01:34 AM using "MacroBookInspector" macro by Walter F. Nickeson.
