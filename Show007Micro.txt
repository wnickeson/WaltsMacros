'MacroName:Show007Micro.2022.04
'MacroDescription:Displays the codes in an 007 field in a bibliographic record for
' microforms in a friendly way for verification and/or correction.
'
' This macro was written by Walter F. Nickeson,
' University of Rochester, Rochester, NY
' wnickeson@library.rochester.edu
'
' with some ideas from Joel Hahn.
'
' Last updated: 22 June 2022.
' Check for the latest versions of this and my other macros at
' https://github.com/wnickeson/WaltsMacros
' Please e-mail me with bug reports or to suggest improvements.
'
' This macro works for me with Connexion client 3.1 and 64-bit Windows 10 Enterprise &
' 64-bit Windows 10 Pro, but no guarantees are promised or implied.
'
'***************************************************************************************#
' How it works: In a record for a microform, run the macro. It looks for the first
' microform 007 field and shows the field's values in a dialog box with drop-down lists
' so the values can be verified and changed if necessary. If the cursor is placed in a
' microform 007, that field is used in the macro. If there is no 007, use the dialog box
' to enter correct values in a new field. Default values for new fields can be set in the
' section below that starts on line 117.
'****************************************************************************************

Option Explicit

Declare Sub FillArrays

Declare Function GetSubfieldValue( InString$, WhichSubfield$ ) As String
Declare Function Dialog1ControlFunction( Id$, Action%, SValue& )

Global Const WARNING_MESSAGE As Integer = 48  'The value to display the "Warning message" icon in a message box.

Global AValue$
Global BValue$
Global DValue$
Global EValue$
Global FValue$
Global FRatio$
Global GValue$
Global HValue$
Global IValue$
Global JValue$
Global WaltsMacros$

Global AddNew

Global BArray( 10 ) As String
Global DArray( 3 )  As String
Global EArray( 10 ) As String
Global FArray( 7 )  As String
Global GArray( 5 )  As String
Global HArray( 7 )  As String
Global IArray( 5 )  As String
Global JArray( 11 ) As String

'****************************************************************************************

Sub Main

Dim CS As Object
On Error Resume Next
Set CS = GetObject( , "Connex.Client" )
On Error GoTo 0
If CS Is Nothing Then Set CS = CreateObject( "Connex.Client" )

Const CRITICAL_MESSAGE     As Integer = 16
Const INFORMATION_MESSAGE  As Integer = 64
Const WARNING_QUERY        As Integer = 32

Const ANSWER_YES           As Integer = 6
Const YES_NO               As Integer = 4

Dim DEFAULT_SUBFIELD_B As String*1
Dim DEFAULT_SUBFIELD_D As String*1
Dim DEFAULT_SUBFIELD_E As String*1
Dim DEFAULT_SUBFIELD_F As String*1
Dim DEFAULT_F_RATIO
Dim DEFAULT_SUBFIELD_G As String*1
Dim DEFAULT_SUBFIELD_H As String*1
Dim DEFAULT_SUBFIELD_I As String*1
Dim DEFAULT_SUBFIELD_J As String*1
Dim SUBFIELD_B         As String*2 : SUBFIELD_B   = Chr$( 223 ) & "b"
Dim SUBFIELD_D         As String*2 : SUBFIELD_D   = Chr$( 223 ) & "d"
Dim SUBFIELD_E         As String*2 : SUBFIELD_E   = Chr$( 223 ) & "e"
Dim SUBFIELD_F         As String*2 : SUBFIELD_F   = Chr$( 223 ) & "f"
Dim SUBFIELD_G         As String*2 : SUBFIELD_G   = Chr$( 223 ) & "g"
Dim SUBFIELD_H         As String*2 : SUBFIELD_H   = Chr$( 223 ) & "h"
Dim SUBFIELD_I         As String*2 : SUBFIELD_I   = Chr$( 223 ) & "i"
Dim SUBFIELD_J         As String*2 : SUBFIELD_J   = Chr$( 223 ) & "j"

Dim Field007String$
Dim FieldData$
Dim Instance%                      : Instance%    = 1
Dim Row%
Dim TypeOfWindow%

Dim Changed                        : Changed      = FALSE
Dim Field007Bool                   : Field007Bool = FALSE

Dim p As Integer

AddNew       = FALSE
WaltsMacros$ = "[Walt's macros] Extras1:Show007Micro"

'****************************************************************************************
' SET THE DEFAULT VALUES FOR NEW 007 FIELDS HERE
' Enter values in lowercase. Input a blank (" ") as the value for optional subfields that
' will not be coded.
DEFAULT_SUBFIELD_B = "d"   'MANDATORY SUBFIELD
DEFAULT_SUBFIELD_D = "a"   'MANDATORY SUBFIELD
DEFAULT_SUBFIELD_E = "f"   'MANDATORY SUBFIELD
DEFAULT_SUBFIELD_F = "u"   'OPTIONAL SUBFIELD
DEFAULT_F_RATIO    = "---" 'OPTIONAL VALUE
DEFAULT_SUBFIELD_G = "b"   'OPTIONAL SUBFIELD
DEFAULT_SUBFIELD_H = "a"   'OPTIONAL SUBFIELD
DEFAULT_SUBFIELD_I = "c"   'REQUIRED IF APPLICABLE
DEFAULT_SUBFIELD_J = "a"   'OPTIONAL SUBFIELD
'****************************************************************************************

' If what is displayed is not a bibliographic record, quit.

TypeOfWindow% = CS.ItemType
Select Case TypeOfWindow%
  Case -1, 3 To 16, 18, 20 To 26
    MsgBox "Sorry, this macro works only in an bibliographic record!", CRITICAL_MESSAGE, WaltsMacros$
    Exit Sub
End Select

' If the cursor is in a microform 007 field, work on that one; otherwise cycle through
' the first 10 007 fields in the record looking for a microform 007.

Row% = CS.CursorRow
If CS.GetFieldLine( Row%, FieldData$ ) = TRUE Then
    If Mid$( FieldData$, 1, 3 ) = "007" And Mid$( FieldData$, 6, 1 ) = "h" Then GoTo GetValues:
End If

Do Until Field007Bool = TRUE Or Instance% = 10
  Field007Bool = CS.GetField( "007", Instance%, FieldData$ )
  If Field007Bool = TRUE Then
      If Mid$( FieldData$, 6, 1 ) = "h" Then
          Exit Do
        Else
          Field007Bool = FALSE
      End If
  End If
  Instance% = Instance% + 1
Loop

If Instance% = 10 Then
    If MsgBox( "No microform 007 field found! Create a new one?", WARNING_QUERY + YES_NO, WaltsMacros$ ) = ANSWER_YES Then
        AddNew  = TRUE
        Changed = TRUE
      Else
        Exit Sub
    End If
End If

' Now find all the subfields and their values. Subfields $b, $d, and $e are mandatory, so
' if they are not found, that means no 007 field exists and a new one is added, with the
' default values given above.

GetValues:

If AddNew = TRUE Then
    BValue$ = DEFAULT_SUBFIELD_B
    DValue$ = DEFAULT_SUBFIELD_D
    EValue$ = DEFAULT_SUBFIELD_E
    FValue$ = DEFAULT_SUBFIELD_F
    FRatio$ = DEFAULT_F_RATIO
    GValue$ = DEFAULT_SUBFIELD_G
    HValue$ = DEFAULT_SUBFIELD_H
    IValue$ = DEFAULT_SUBFIELD_I
    JValue$ = DEFAULT_SUBFIELD_J
  Else
    BValue$ = GetSubfieldValue( FieldData$, SUBFIELD_B )
    DValue$ = GetSubfieldValue( FieldData$, SUBFIELD_D )
    EValue$ = GetSubfieldValue( FieldData$, SUBFIELD_E )
    FValue$ = GetSubfieldValue( FieldData$, SUBFIELD_F )
    GValue$ = GetSubfieldValue( FieldData$, SUBFIELD_G )
    HValue$ = GetSubfieldValue( FieldData$, SUBFIELD_H )
    IValue$ = GetSubfieldValue( FieldData$, SUBFIELD_I )
    JValue$ = GetSubfieldValue( FieldData$, SUBFIELD_J )
End If

FillArrays

' The main dialog box, showing the present values in the 007 field and offering drop-down
' lists for changing those values, or adding new ones.

ShowValues:

Begin Dialog Dialog1Definition  274, 170, WaltsMacros$, .Dialog1ControlFunction
  PushButton    152, 146,  52,  14, "",                               .OK
  CancelButton  212, 146,  52,  14
  DropListBox   112,  10, 152, 104, BArray(),                         .B
  DropListBox   112,  26, 152,  48, DArray(),                         .D
  DropListBox   112,  42, 152, 104, EArray(),                         .E
  DropListBox   112,  58, 128,  80, FArray(),                         .F
  TextBox       244,  58,  19,  12,                                   .FRatio
  DropListBox   112,  74, 152,  64, GArray(),                         .G
  DropListBox   112,  90, 152,  80, HArray(),                         .H
  DropListBox   112, 106, 152,  64, IArray(),                         .I
  DropListBox   112, 122, 152, 112, JArray(),                         .J
  Text           10,  12,  92,   8, "Specific material designation:"
  Text           10,  28,  92,   8, "Positive/negative aspect:"
  Text           10,  44,  92,   8, "Dimensions:"
  Text           10,  60,  96,   8, "*Reduction ratio range + ratio:"
  Text           10,  76,  92,   8, "*Color:"
  Text           10,  92,  92,   8, "*Emulsion on film:"
  Text           10, 108,  92,   8, "Generation:"
  Text           10, 124,  92,   8, "*Base of film:"
  Text           28, 140,  48,   8, "*Optional fields"
End Dialog

Dim Dialog1 As Dialog1Definition

If Dialog( Dialog1 ) = 0 Then Exit Sub

' Build a new 007 field with the values derived from the dialog

BValue$ = Left$( BArray( Dialog1.B ), 1 )
DValue$ = Left$( DArray( Dialog1.D ), 1 )
EValue$ = Left$( EArray( Dialog1.E ), 1 )
FValue$ = Left$( FArray( Dialog1.F ), 1 )
GValue$ = Left$( GArray( Dialog1.G ), 1 )
HValue$ = Left$( HArray( Dialog1.H ), 1 )
IValue$ = Left$( IArray( Dialog1.I ), 1 )
JValue$ = Left$( JArray( Dialog1.J ), 1 )
FRatio$ = Dialog1.FRatio

Field007String$ = "007  h " & SUBFIELD_B & " " & BValue$ & " " & SUBFIELD_D & " " & DValue$ & " " & SUBFIELD_E & " " & EValue$
If FVAlue$ <> " " Then Field007String$ = Field007String$ & " " & SUBFIELD_F & " " & FValue$ & FRatio$
If GVAlue$ <> " " Then Field007String$ = Field007String$ & " " & SUBFIELD_G & " " & GValue$
If HValue$ <> " " Then Field007String$ = Field007String$ & " " & SUBFIELD_H & " " & HValue$
If IValue$ <> " " Then Field007String$ = Field007String$ & " " & SUBFIELD_I & " " & IValue$
If JValue$ <> " " Then Field007String$ = Field007String$ & " " & SUBFIELD_J & " " & JValue$

If AddNew Then
    If CS.AddField( 1, Field007String$ ) = FALSE Then
        MsgBox "Sorry, could not add microform 007.", CRITICAL_MESSAGE, WaltsMacros$
    End If
  Else
    If CS.SetField( Instance%, Field007String$ ) Then
        MsgBox "The microform 007 field was changed.", INFORMATION_MESSAGE, WaltsMacros$
      Else
        MsgBox "Sorry, could not change microform 007.", CRITICAL_MESSAGE, WaltsMacros$
    End If
End If

End Sub

'****************************************************************************************

Sub FillArrays

BArray( 0 )   = "a - Aperture card"
BArray( 1 )   = "b - Microfilm cartridge"
BArray( 2 )   = "c - Microfilm cassette"
BArray( 3 )   = "d - Microfilm reel"
BArray( 4 )   = "e - Microfiche"
BArray( 5 )   = "f - Microfiche cassette"
BArray( 6 )   = "g - Microopaque"
BArray( 7 )   = "h - Microfilm slip"
BArray( 8 )   = "j - Microfilm roll"
BArray( 9 )   = "u - Unspecified"
BArray( 10 )  = "z - Other"

DArray( 0 )  = "a - Positive"
DArray( 1 )  = "b - Negative"
DArray( 2 )  = "m - Mixed polarity"
DArray( 3 )  = "u - Unknown"

EArray( 0 )  = "a - 8 mm"
EArray( 1 )  = "d - 16 mm"
EArray( 2 )  = "f - 35 mm"
EArray( 3 )  = "g - 70 mm"
EArray( 4 )  = "h - 105 mm"
EArray( 5 )  = "l - 3 × 5 in. (8 × 13 cm)"
EArray( 6 )  = "m - 4 × 6 in. (11 × 15 cm)"
EArray( 7 )  = "o - 6 × 9 in. (16 × 23 cm)"
EArray( 8 )  = "p - 3 1/4 × 7 3/8 in. (9 × 19 cm)"
EArray( 9 )  = "u - Unknown"
EArray( 10 ) = "z - Other"

FArray( 0 )  = " "
FArray( 1 )  = "a - Low reduction (less than 16x)"
FArray( 2 )  = "b - Normal reduction (16-30x)"
FArray( 3 )  = "c - High reduction (31-60x)"
FArray( 4 )  = "d - Very high reduction (61-90x)"
FArray( 5 )  = "e - Ultra high reduction (over 90x)"
FArray( 6 )  = "u - Unknown"
FArray( 7 )  = "v - Reduction ratio varies"

GArray( 0 )  = " "
GArray( 1 )  = "b - Black-and-white"
GArray( 2 )  = "c - Multicolored"
GArray( 3 )  = "m - Mixed"
GArray( 4 )  = "u - Unknown"
GArray( 5 )  = "z - Other"

HArray( 0 )  = " "
HArray( 1 )  = "a - Silver halide"
HArray( 2 )  = "b - Diazo"
HArray( 3 )  = "c - Vesicular"
HArray( 4 )  = "m - Mixed"
HArray( 5 )  = "n - Not applicable"
HArray( 6 )  = "u - Unknown"
HArray( 7 )  = "z - Other"

IArray( 0 )  = " "
IArray( 1 )  = "a - First generation master"
IArray( 2 )  = "b - Printing master"
IArray( 3 )  = "c - Service copy"
IArray( 4 )  = "m - Mixed generation"
IArray( 5 )  = "u - Unknown"

JArray( 0 )  = " "
JArray( 1 )  = "a - Safety base, undetermined"
JArray( 2 )  = "c - Safety base, acetate undetermined"
JArray( 3 )  = "d - Safety base, diacetate"
JArray( 4 )  = "i - Nitrate base"
JArray( 5 )  = "m - Mixed base, nitrate and safety"
JArray( 6 )  = "n - Not applicable"
JArray( 7 )  = "p - Safety base, polyester"
JArray( 8 )  = "r - Safety base, mixed"
JArray( 9 )  = "t - Safety base, triacetate"
JArray( 10)  = "u - Unknown"
JArray( 11 ) = "z - Other"

End Sub

'****************************************************************************************

Function GetSubfieldValue( InString$, WhichSubfield$ ) As String

Dim p As Integer

p = Instr( InString$, WhichSubfield$ )
If p <> 0 Then
    GetSubfieldValue = Mid$( InString$, p + 3, 1 )
    If Right$( WhichSubfield$, 1 ) = "f" Then
        FRatio$ = Mid$( InString$, p + 4, 3 )
    End If
  Else
    GetSubfieldValue = " "
End If

End Function

'****************************************************************************************

Function ChangeTest( WhichControl$ ) As Integer

Dim Changed                  : Changed = FALSE

Select Case WhichControl$

  Case "B"
    If Left$( DlgText( "B" ), 1 ) <> BValue$ Then Changed = TRUE
  Case "D"
    If Left$( DlgText( "D" ), 1 ) <> DValue$ Then Changed = TRUE
  Case "E"
    If Left$( DlgText( "E" ), 1 ) <> EValue$ Then Changed = TRUE
  Case "F"
    If Left$( DlgText( "F" ), 1 ) <> FValue$ Then Changed = TRUE
  Case "G"
    If Left$( DlgText( "G" ), 1 ) <> GValue$ Then Changed = TRUE
  Case "H"
    If Left$( DlgText( "H" ), 1 ) <> HValue$ Then Changed = TRUE
  Case "I"
    If Left$( DlgText( "I" ), 1 ) <> IValue$ Then Changed = TRUE
  Case "J"
    If Left$( DlgText( "J" ), 1 ) <> JValue$ Then Changed = TRUE
  Case "FRatio"
    If Trim$( DlgText( "FRatio" ) ) <> FRatio$ Then Changed = TRUE

End Select

If Changed Then
    ChangeTest = -1
  Else
    ChangeTest = 0
End If

End Function

'****************************************************************************************

Function Dialog1ControlFunction( Id$, Action%, SValue& )

Const COMBO_BOX_CHANGE As Integer = 3   'The value for the state of a dialog box when a control changes.
Const CONTROL_CHANGE   As Integer = 2   'The value of the dialog box function parameter "Action%" when a control changes.
Const DISABLED         As Integer = 0   'The value to make a dialog box control disabled.
Const DLG_EXIT_WITH_OK As Integer = -1  'The value to close the dialog box as if the OK button were clicked.
Const ENABLED          As Integer = 1   'The value to make a dialog box control enabled.
Const INITIALIZE       As Integer = 1   'The value of the dialog box function parameter "Action%" when the dialog box opens.
Const KEEP_DLG_OPEN    As Integer = -1  'The value of the dialog box function to keep a dialog box open.

Dim QUOTE As String *2       : QUOTE        = Chr$( 034 )

Dim LenTempString%
Dim ReductionRatio$
Dim ReductionRatioRange$
Dim TempString$
Dim TempStringVal%
Dim TestChar$
Dim ValueString$

Dim Changed                  : Changed      = TRUE
Dim ProblemInput             : ProblemInput = FALSE

Dim i As Integer

Select Case Action%

  Case INITIALIZE

    DlgText   "B",      BValue$
    DlgText   "D",      DValue$
    DlgText   "E",      EValue$
    DlgText   "F",      FValue$
    DlgText   "FRatio", FRatio$
    DlgText   "G",      GValue$
    DlgText   "H",      HValue$
    DlgText   "I",      IValue$
    DlgText   "J",      JValue$
    If AddNew Then
        DlgEnable "OK",     ENABLED
        DlgText   "OK",     "Add 007"
      Else
        DlgEnable "OK",     DISABLED
        DlgText   "OK",     "Change 007"
    End If

  Case CONTROL_CHANGE

    Select Case Id$

      Case "B", "D", "E", "F", "G", "H", "I", "J", "FRatio"

        If ChangeTest( "B" ) = 0 Then
            If ChangeTest( "D" ) = 0 Then
                If ChangeTest( "E" ) = 0 Then
                    If ChangeTest( "F" ) = 0 Then
                        If ChangeTest( "FRatio" ) = 0 Then
                            If ChangeTest( "G" ) = 0 Then
                                If ChangeTest( "H" ) = 0 Then
                                    If ChangeTest( "I" ) = 0 Then
                                        If ChangeTest( "J" ) = 0 Then
                                            Changed = FALSE
                                        End If
                                    End If
                                End If
                            End If
                        End If
                    End If
                End If
            End If
        End If
        If Changed Then
            DlgEnable "OK",     ENABLED
          Else
            DlgEnable "OK",     DISABLED
        End If

      Case "OK"

' The chief thing to do when the OK button is clicked is make sure the input of the
' reduction ratio is valid. First, make sure the string is three characters in length. If
' it's shorter, pad the string with leading hyphens. If it's longer, display a warning.

        TempString$    = Trim$( DlgText( "FRatio" ) )
        LenTempString% = Len( TempString$ )

        Select Case LenTempString%

          Case 0
            ReductionRatio$ = "---"
          Case 1
            TempString$ = "--" & TempString$
          Case 2
            TempString$ = "-" & TempString$
          Case Is > 3
            MsgBox "Please input only THREE characters for the reduction ratio.", WARNING_MESSAGE, WaltsMacros$
            DlgFocus  "FRatio"
            GoTo ProblemInput:

        End Select

' Then check that the characters entered are only digits or the hyphen.

        For i = 1 To 3
          TestChar$ = Mid$( TempString$, i, 1 )
          If TestChar$ Like "[!0-9-]" Then
              MsgBox "Please entere only digits or a hyphen for the reduction ratio.", WARNING_MESSAGE, WaltsMacros$
              DlgFocus  "FRatio"
              GoTo ProblemInput:

' A hyphen is kept if it follows a digit other than zero; otherwise it's converted to a
' zero. A parallel string containing only digits must be kept to properly evaluate the
' value of the string, so a hyphen is always converted to zero in it.

            Else
              If TestChar$ = "-" Then
                  ValueString$ = ValueString$ & "0"
                  If i = 1 Then
                      Mid$( TempString$, i, 1 ) = "0"
                    Else
                      If Mid$( TempString$, i - 1, 1 ) = "0" Then
                          Mid$( TempString$, i, 1 ) = "0"
                      End If
                  End If
                Else
                  ValueString$ = ValueString$ & TestChar$
              End If
          End If
        Next i

' The reduction ratio must correspond appropriately to the reduction ratio range. If the
' range is unknown, so is the ratio; so if one of the quantities is known, the other must
' be also. Otherwise, show a warning. Also, if the reduction ratio range does not
' correspond with the actual reduction ratio, show a warning.

        ReductionRatioRange$ = Left$( DlgText( "F" ), 1 )
        If ReductionRatioRange$ = "u" And TempString$ <> "---" Then
            MsgBox "If the reduction ratio is known, the reduction ratio range cannot be " & QUOTE & "u" & QUOTE & ".", WARNING_MESSAGE, WaltsMacros$
            DlgFocus  "F"
            GoTo ProblemInput:
          ElseIf ReductionRatioRange$ <> "u" And TempString$ = "---" Then
            MsgBox "If the reduction ratio range is known, the reduction ratio range cannot be " & QUOTE & "---" & QUOTE & ".", WARNING_MESSAGE, WaltsMacros$
            DlgFocus  "FRatio"
            GoTo ProblemInput:
          Else
            TempStringVal% = Val( ValueString$ )
            If ReductionRatioRange$ = "a" And TempStringVal > 15 Or _
                   ReductionRatioRange$ = "b" And ( TempStringVal% < 16 Or TempStringVal% > 30 ) Or _
                   ReductionRatioRange$ = "c" And ( TempStringVal% < 31 Or TempStringVal% > 60 ) Or _
                   ReductionRatioRange$ = "d" And ( TempStringVal% < 61 Or TempStringVal% > 90 ) Or _
                   ReductionRatioRange$ = "e" And TempStringVal% < 91 Then
                MsgBox "The reduction ratio range does not match the actual reduction ratio.", WARNING_MESSAGE, WaltsMacros$
                DlgFocus  "FRatio"
                GoTo ProblemInput:
            End If
        End If
        DlgText   "F",      ReductionRatioRange$
        DlgText   "FRatio", TempString$
        DlgEnd    DLG_EXIT_WITH_OK

ProblemInput:

        Dialog1ControlFunction = KEEP_DLG_OPEN

    End Select


  Case COMBO_BOX_CHANGE

    If Id$ = "FRatio" Then
        ReductionRatio$ = Trim$( DlgText( "FRatio" ) )
        If ReductionRatio$ = FRatio$ Then
            DlgEnable "OK",     DISABLED
          Else
            DlgEnable "OK",     ENABLED
        End If
    End If

End Select

End Function
'57521176
'
'Macro name: Show007Micro
'Macro book: C:\Users\wnickeson\AppData\Roaming\OCLC\Connex\Macros\Extras1.mbk
'Saved: 4/17/2023 9:54:20 AM using "MacroBookInspector" macro by Walter F. Nickeson.
